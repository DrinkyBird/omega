// Actors common to all mechs -- Medicwep Version
actor MechPlayer_Base : PlayerPawn {
	damagefactor "Fist", 0.50
	damagefactor "Bullet", 0.50
	damagefactor "Rifle", 0.50
	damagefactor "Explosion", 1.0 // This makes things easy to calculate...
	damagefactor "Fire", 0.50
	damagefactor "Chemical", 0.75 // Chemical weapons are also pretty good against mechs.
	damagefactor "ChemicalSpecial", 0.0
	damagefactor "Laser", 0.50
	damagefactor "RazorWire", 2.00
	damagefactor "ExplosionMech", 1.50 //Arty 80mms do 410. Enough to leave an orca at 90 HP.
	damagefactor "Time", 1.00
	damagefactor "Disarm", 0.00
	damagefactor "Deconstruction", 0.00
	damagefactor "MassDriver", 0.50
	damagefactor "Mechstomp", 0.0
	damagefactor "Monster", 1.0
	damagefactor "Falling", 12.0
	maxstepheight 40
	player.jumpz 0
	player.damagescreencolor "00 00 00"
	player.soundclass "mech"
	painchance 0
	mass 0x7FFFFFFF
	-TELESTOMP
	-NOBLOCKMONST
	+NOSKIN
	bloodtype "HHMetalHit"
	bloodcolor "White"
}


actor MechDeathSmoke {
	PROJECTILE
	-RANDOMIZE
	-NOGRAVITY
	radius 11
	height 8
	speed 1
	damage (15)
	gravity 0.3
	scale 0.03
	damagetype "Explosion"
	obituary "%o was blown up by %k's junk!"
	states {
	Spawn:
		TNT1 A 6 
		TNT1 A 2 A_SpawnItemEx("PulseBallSmokePuff", 0, 0, 0, 0, 0, 1)
		wait
	Death:
		TNT1 A 0 A_PlaySound ("mech/explode/junk")
		TNT1 A 0 A_Explode (35, 128, 1)
		TNT1 A 0 A_SpawnItemEx ("ExplosionMain")
		stop
	}
}

actor MechStepSmokePuffLarge : MechStepSmokePuff {scale 2.5}

actor MechStepDamageWeak {
	RenderStyle None
	+RANDOMIZE
	+NOCLIP
	+NODAMAGETHRUST
	-TELESTOMP
	PROJECTILE
	Speed 1
	Damage 0
	Scale 0.7
	damagetype "MechStomp"
	states {
	Spawn:
		TNT1 A 0
		ROB1 D 0 Radius_Quake(2,8,0,2,0)
		PUFF A 0 A_SpawnItemEx("StepSound", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		TNT1 A 10 bright A_Explode(32,128,0)
		stop
	}  
}

actor MechStepDamageStrong : MechStepDamageWeak {
	-TELESTOMP
	states {
	Spawn:
		TNT1 A 0
		ROB1 D 0 Radius_Quake(3,10,0,4,0)
		PUFF A 0 A_SpawnItemEx("StepSound", 0, 0, 0, 0, 0, 0, 0, 128, 0)
		TNT1 A 10 bright A_Explode(64,128,0)
		stop
	}  
}

actor MechStepGuardian : MechStepDamageWeak {
	+EXTREMEDEATH
	-TELESTOMP
	damagetype "MechStomp"
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 0 Radius_Quake (3, 10, 0, 4, 0)
		TNT1 A 10 bright A_Explode (128, 256, 0)
		stop
	}  
}

actor MechStepDamageOrca : MechStepDamageWeak {
	-TELESTOMP
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 10 bright A_Explode(16,64,0)
		stop
	}
}
actor StepSound {
	+CLIENTSIDEONLY
	-TELESTOMP
	radius 1
	height 1
	+noclip
	States {
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_PlaySound("mech/step")
		stop
	}
}

actor MechReloading : Inventory {inventory.maxamount 20}

actor MechAmmoStuffSmall : CustomInventory {
	+ALWAYSPICKUP
	states {
	Pickup:
		TNT1 A 0 A_GiveInventory("MechRocketAmmo", 2)
		TNT1 A 0 A_GiveInventory("ShortRangeMissileAmmo", 2)
		TNT1 A 0 A_GiveInventory("Wolverine-MinigunAmmo", 4)
		TNT1 A 0 A_GiveInventory("Titan-RocketAmmo", 2)
		TNT1 A 0 A_GiveInventory("Titan-80mmAmmo", 1)
		TNT1 A 0 A_GiveInventory("Titan-FlamerAmmo", 4)
		TNT1 A 0 A_GiveInventory("Raven-RocketAmmo", 2)
		TNT1 A 0 A_GiveInventory("RavenAC2Ammo", 5)
		TNT1 A 0 A_GiveInventory("Orca-RocketAmmo", 1)
		TNT1 A 0 A_GiveInventory("Orca-MinigunAmmo", 4)
		TNT1 A 0 A_GiveInventory("FlamerAmmo", 5)
		TNT1 A 0 A_GiveInventory("ShortRangeMissileAmmo", 2)
		TNT1 A 0 A_GiveInventory("ArtilleryShellAmmo", 2)
		TNT1 A 0 A_GiveInventory("MachineGunAmmo", 4)
		TNT1 A 0 A_GiveInventory("GeneporiumCharge", 3)
		TNT1 A 0 A_GiveInventory("NukeAmmo", 1)
		stop
	}
}

actor MechEjectButton : CustomInventory {
	+INVBAR
	inventory.maxamount 1
	inventory.icon "ICO_EJCT"
	tag "Eject mech"
	states {
	Spawn:
		TNT1 A 0
		stop
	Use:
		TNT1 A 0 ACS_ExecuteAlways (697, 0)
		stop
	}
}

actor LongRangeMissile {
	PROJECTILE
	-RANDOMIZE
	-NOGRAVITY
	+ROCKETTRAIL
	radius 11
	height 8
	speed 45
	damage (30) //60 mech, 18 inf. Full LRM-20: 1200mech, 360inf.
	deathsound "weapons/rocklx"
	damagetype "Explosion"
	decal "Scorch"
	scale 0.8
	gravity 0.025
	States {
	Spawn:
		MISL AA 0 A_PlaySound ("ambient/rocket", CHAN_BODY, 1.0, 1)
		MISL A -1 bright
		stop
	Death:
		TNT1 A 0 A_StopSound (CHAN_BODY)
		TNT1 A 0 A_Explode (30, 128, 0)
		TNT1 A 0 A_SpawnItemEx ("FlameDecay", random (-16,16), random (-16,16), 4, 0, 0, 0, 0, 0, 160)
		TNT1 A 5 A_SpawnItemEx ("ExplosionMain")
		stop
	}
}

actor ShortRangeMissile : LongRangeMissile {
	speed 35
	damage (20) //70 mech, 21 inf. Full SRM-15: 1050mech, 315inf.
	deathsound "weapons/rocklx"
	damagetype "ExplosionMech"
	scale 0.85
	states {
	Spawn:
		MISL AA 0 A_PlaySound ("ambient/rocket", CHAN_BODY, 1.0, 1)
		MISL A 28 bright
		MISL A -1 bright A_Gravity
	Death:
		TNT1 A 0 A_StopSound (CHAN_BODY)
		TNT1 A 0 A_Explode (10, 256, 0)
		TNT1 A 0 A_Explode (20, 128, 0)
		TNT1 A 0 A_Explode (20, 128, 0)
		TNT1 A 0 A_SpawnItemEx ("FlameDecay", random (-16,16), random (-16,16), 4, 0, 0, 0, 0, 0, 80)
		TNT1 A 5 A_SpawnItemEx ("ExplosionMain")
		stop
	}
}

// Ammo types
actor ShortRangeMissileAmmo : Ammo {
	inventory.amount 0
	inventory.maxamount 150
	ammo.backpackamount 10
	ammo.backpackmaxamount 150
}

actor MechExplosionSmall {
	obituary "%o was blown up by %k's mech."
	damagetype "Monster"
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound ("mech/explode/medium1", 1)
		TNT1 A 0 A_PlaySound ("mech/explode/medium2", 2)
		TNT1 A 0 A_PlaySound ("mech/explode/medium1", 3, 0.5)
		TNT1 A 0 A_PlaySound ("mech/explode/medium2", 4, 0.5)
		TNT1 A 0 A_Explode (50, 512)
		TNT1 A 0 A_Explode (350, 128)
		TNT1 A 0 A_Explode (350, 256)
		TNT1 AAAAA 0 A_SpawnItemEx("MechDeathSmoke", 0, 0, random(64,120), random(-5,5), random(-5,5), random(9,14), 0, 0, 64)
		TNT1 A 8 A_SpawnItemEx ("ExplosionMainLarge")
		TNT1 A 8 A_SpawnItemEx ("ExplosionMainLarge", 12, 16, 64)
		TNT1 A 8 A_SpawnItemEx ("ExplosionMainLarge", 14, 32, 96)
		TNT1 A 8 A_SpawnItemEx ("ExplosionMainLarge", 26, 48, 128)
		TNT1 A 5
		Stop
	}
}

actor MechExplosionLarge {
	obituary "%o was blown up by %k's big mech."
	damagetype "Monster"
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound ("mech/explode/large1", 1)
		TNT1 A 0 A_PlaySound ("mech/explode/large2", 2)
		TNT1 A 0 A_PlaySound ("mech/explode/large1", 3, 0.5)
		TNT1 A 0 A_PlaySound ("mech/explode/large2", 4, 0.5)
		TNT1 A 0 A_Explode (100, 512)
		TNT1 A 0 A_Explode (700, 128)
		TNT1 A 0 A_Explode (700, 256)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("MechDeathSmoke", 0, 0, random(64,120), random(-5,5), random(-5,5), random(9,14), 0, 0, 64)
		TNT1 A 8 A_SpawnItemEx ("ExplosionMainLarge")
		TNT1 A 8 A_SpawnItemEx ("ExplosionMainLarge", 12, 16, 64)
		TNT1 A 8 A_SpawnItemEx ("ExplosionMainLarge", 14, 32, 96)
		TNT1 A 8 A_SpawnItemEx ("ExplosionMainLarge", 26, 48, 128)
		TNT1 A 8 A_SpawnItemEx ("ExplosionMainLarge", 38, 72, 160)
		TNT1 A 8 A_SpawnItemEx ("ExplosionMainLarge", 50, 96, 192)
		Stop
	}
}
// Shotgun
actor Boomstick : AOWWeapon replaces Shotgun 21080 {
	//$Category Weapons
	Tag "Shotgun"
	Obituary "$OB_SHOTGUN"
	Attacksound "weapons/shotgf"
	Inventory.PickupMessage "You got the shotgun!"
	Weapon.SelectionOrder 17
	Weapon.KickBack 80
	Weapon.AmmoType "ShotgunAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.PreferredSkin "MarineShotgun"
	States {
	Ready:
		SHTG A 1 A_WeaponReady
		wait
	Deselect:
		SHTG A 0 A_Lower
		SHTG A 1 A_Lower
		loop
	Select:
		SHTG A 0 A_Raise
		SHTG A 1 A_Raise
		loop
	Fire:
		TNT1 A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "Ready")
		TNT1 A 0 A_JumpIfInventory ("InMenu", 1, "Ready")
		SHTG A 3
		SHTG A 0 A_GunFlash
		SHTG A 0 Bright A_FireBullets (5.5, 5.5, 2, 6, "HandgunPuff",0,350)
		SHTG A 0 Radius_Quake(3,random(3,5),0,1,0)
		SHTG A 0 Bright A_FireBullets (5.5, 5.5, 6, 4, "HandgunPuff",1,1000)
		SHTG A 1 Bright A_ZoomFactor(0.989)
		SHTG A 1 Bright A_ZoomFactor(random(978,986)*0.001)
		SHTG A 2 Bright A_ZoomFactor(0.99)
		SHTG A 1 Bright A_ZoomFactor(1.005)
		SHTG B 2 A_ZoomFactor(1.02)
		SHTG B 2 A_ZoomFactor(1)
		SHTG C 4
		SHTG D 4 A_SpawnItemEx ("ShotgunShellCasing", 12, 9, 33,
			random (-2, -4), random (2, 3), random (1, 2), random (45, 60), 128)
		SHTG CB 4
		SHTG A 3
		SHTG A 7 A_ReFire
		goto Ready
	Flash:
		SHTF A 2 Bright A_Light1
		SHTF B 3 Bright A_Light2
		SHTF B 0 A_Light0
		stop
	Spawn:
		SHOT A -1
		stop
	}
}
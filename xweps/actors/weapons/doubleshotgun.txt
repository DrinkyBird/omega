// Double-barreled Shotgun
actor DoubleShotgun : AOWWeapon replaces SuperShotgun 21086 {
	//$Category Weapons
	Inventory.PickupMessage "You got the double shotgun!"
	Obituary "$OB_DOUBLESHOTGUN" // "%o was splattered by %k's double shotgun."
	AttackSound "weapons/sshotf"
	DropItem "Boomstick"
	Weapon.SelectionOrder 16
	Weapon.Kickback 30
	Weapon.AmmoType "ShotgunAmmo"
	Weapon.AmmoUse 2
	Weapon.AmmoGive 0
	Weapon.PreferredSkin "MarineSSG"
	Tag "Double Shotgun"
	states {
	Ready:
		SHT2 A 1 A_WeaponReady
		loop
	Deselect:
		SHT2 A 0 A_Lower
		SHT2 A 1 A_Lower
		loop
	Select:	
		SHT2 A 0 A_Raise
		SHT2 A 1 A_Raise
		loop
	Fire:
		TNT1 A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "Ready")
		TNT1 A 0 A_JumpIfInventory ("InMenu", 1, "Ready")
		SHT2 A 3
		SHT2 A 0 Radius_Quake(3,7,0,1,0)
		SHT2 A 0 Bright A_GunFlash
		SHT2 A 0 Bright A_FireBullets (13.5, 9.4, 20, 5, "HandgunPuff", 1, 1024)
		SHT2 A 2 A_ZoomFactor(0.973)
		SHT2 A 5 A_ZoomFactor(1.02)
		SHT2 B 7 A_ZoomFactor(1)
		SHT2 C 7 A_CheckReload
		SHT2 DD 0 A_SpawnItemEx("ShotgunShellCasing",8,8,31,random(2,4),random(2,3),random(1,3),156)
		SHT2 D 7 A_OpenShotgun2
		SHT2 E 7
		SHT2 F 7 A_LoadShotgun2
		SHT2 G 6
		SHT2 H 6 A_CloseShotgun2
		SHT2 A 5 A_ReFire
		goto Ready
	Flash:
		SHT2 I 3 Bright A_Light1
		SHT2 J 4 Bright A_Light2
		TNT1 A 28 A_Light0
		stop
	Spawn:
		TNT1 A 0
		stop
	}
}
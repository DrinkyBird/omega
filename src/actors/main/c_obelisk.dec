/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * Obelisk of Light
 *
 * The Obelisk is a tower-like structure that guards the base. It exists at
 * game start and is one of the five structures that must be destroyed to win
 * the game.
 *
 * The Obelisk structure is built of map geometry. The actors defined here are
 * invisible actors that are the functional bit in the obelisk. They're monsters
 * which shoot the plasma blast at enemies they see.
 */

actor ObeliskEye 21214 {
	//$Category Buildings
	MONSTER
	-ISMONSTER
	-SOLID
	-FRIENDLY
	+FLOORCLIP
	+NOBLOOD
	+NOICEDEATH
	+NOCLIP
	+NOTARGET
	+LOOKALLAROUND
	+NEVERRESPAWN
	+NOGRAVITY
	+NOINTERACTION
	Health 99999
	Radius 16
	Height 64
	Mass 999999
	Speed 0
	PainChance 0
	Tag "Obelisk of Light"
	Obituary "$OB_OBELISK"
	DesignatedTeam 1

	States {
	Spawn:
		TNT1 A 0
		TNT1 A 0 Thing_ChangeTID( 0, TID_OBELISK_EYE )
		TNT1 A 0 A_ChangeFlag("ISMONSTER",1)
		TNT1 A 0 A_GiveInventory("IsRed",1)
		TNT1 A 0 A_TakeInventory("ObeliskCharged",1)
		// If the obelisk respawned, it needs to deactivate.
		TNT1 A 0 A_JumpIfInventory( "PleaseRespawn", 1, "StopAndWait" )
		TNT1 A 8 A_Look
		wait

	StopAndWait:
		TNT1 A -1

	See:
		TNT1 A 0 A_Chase("","",CHF_DONTMOVE)
		TNT1 A 0 A_GiveInventory("IsRed",1)
		TNT1 A 0 A_TakeInventory("ObeliskCharged",1)
		TNT1 A 3 A_FaceTarget
		TNT1 A 2 A_JumpIfTargetInLOS("Missile")
		TNT1 A 2 A_ClearTarget
		goto Spawn
	See2:
		TNT1 A 2 A_ClearTarget
		goto See
	MissileCheck:
		TNT1 AA 0 A_JumpIfInTargetInventory("IsBlue",1,"MissileGo")
		TNT1 A 0 
		goto See
	Missile:
		TNT1 A 0 
		TNT1 A 1 A_JumpIfInTargetInventory("IsRed",1,"See2")
		TNT1 A 1 A_JumpIfCloser(30000,"MissileGo")
		TNT1 A 0 
		goto See2
	MissileGo:
		TNT1 A 0 A_JumpIfInTargetInventory("IsMech",1,"MissileGoMech")
		TNT1 A 0 A_JumpIfInventory("ObeliskCharged",1,"MissileCharged")
		TNT1 A 0 A_FaceTarget
		TNT1 A 70 A_PlaySoundEx("obelisk/charge","Voice",0)
		TNT1 A 0 A_GiveInventory("ObeliskCharged",15)
		TNT1 A 0 A_CposRefire
		TNT1 A 0 A_PlaySoundEx("obelisk/fire","Weapon",0)
		TNT1 A 0 A_PlaySoundEx("obelisk/charge","Body",0)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ObbyFlare")
		TNT1 A 0 A_CustomMissile("ObeliskShotRedInfantry",12,0)
		TNT1 A 70 A_FaceTarget
		TNT1 A 0 A_TakeInventory("ObeliskCharged",15)
		TNT1 A 0 A_PlaySoundEx("obelisk/charge","Voice",0)
		goto See
	MissileCharged:
		TNT1 A 15
		TNT1 A 0 A_PlaySoundEx("obelisk/fire","Weapon",0)
		TNT1 A 0 A_PlaySoundEx("obelisk/charge","Body",0)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("ObeliskShotRedInfantry",12,0)
		TNT1 A 70 A_FaceTarget
		TNT1 A 0 A_TakeInventory("ObeliskCharged",15)
		TNT1 A 0 A_PlaySoundEx("obelisk/charge","Voice",0)
		goto See
	MissileGoMech:
		TNT1 A 0 A_JumpIfInventory("ObeliskCharged",1,"MissileChargedMech")
		TNT1 A 0 A_FaceTarget
		TNT1 A 70 A_PlaySoundEx("obelisk/charge","Voice",0)
		TNT1 A 0 A_GiveInventory("ObeliskCharged",15)
		TNT1 A 0 A_CposRefire
		TNT1 A 0 A_PlaySoundEx("obelisk/fire","Weapon",0)
		TNT1 A 0 A_PlaySoundEx("obelisk/charge","Body",0)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ObbyFlare")
		TNT1 A 0 A_CustomMissile("ObeliskShotRed",12,0)
		TNT1 A 70 A_FaceTarget
		TNT1 A 0 A_TakeInventory("ObeliskCharged",15)
		TNT1 A 0 A_PlaySoundEx("obelisk/charge","Voice",0)
		goto See
	MissileChargedMech:
		TNT1 A 15
		TNT1 A 0 A_PlaySoundEx("obelisk/fire","Weapon",0)
		TNT1 A 0 A_PlaySoundEx("obelisk/charge","Body",0)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("ObeliskShotRed",12,0)
		TNT1 A 70 A_FaceTarget
		TNT1 A 0 A_TakeInventory("ObeliskCharged",15)
		TNT1 A 0 A_PlaySoundEx("obelisk/charge","Voice",0)
		goto See

	Death:
		TNT1 A 35 A_JumpIfInventory( "PleaseRespawn", 1, "Respawn" )
		wait

	Respawn:
		TNT1 A -1 A_Respawn
	}
}

actor TeslaCoilEye 21213 {
	//$Category Buildings
	MONSTER
	-ISMONSTER
	-SOLID
	-FRIENDLY
	+FLOORCLIP
	+NOBLOOD
	+NOICEDEATH
	+NOCLIP
	+NOTARGET
	+LOOKALLAROUND
	+NEVERRESPAWN
	+NOGRAVITY
	+NOINTERACTION
	Health 99999
	Radius 16
	Height 64
	Mass 999999
	Speed 0
	PainChance 0
	DesignatedTeam 0
	Tag "Tesla Coil"
	Obituary "$OB_TESLACOIL"
	States {
	Spawn:
		TNT1 A 0
		TNT1 A 0 Thing_ChangeTID( 0, TID_TESLACOIL_EYE )
		TNT1 A 0 A_ChangeFlag("ISMONSTER",1)
		TNT1 A 0 A_GiveInventory("IsBlue",1)
		// If it respawned, it needs to deactivate.
		TNT1 A 0 A_JumpIfInventory( "PleaseRespawn", 1, "StopAndWait" )
		TNT1 A 0 A_TakeInventory("ObeliskCharged",1)
		TNT1 A 8 A_Look
		wait

	StopAndWait:
		TNT1 A -1

	See:
		TNT1 A 0 A_Chase("","",CHF_DONTMOVE)
		TNT1 A 0 A_GiveInventory("IsBlue",1)
		TNT1 A 0 A_TakeInventory("ObeliskCharged",1)
		TNT1 A 3 A_FaceTarget
		TNT1 A 2 A_JumpIfTargetInLOS("Missile")
		TNT1 A 2 A_ClearTarget
		goto Spawn
	
	See2:
		TNT1 A 2 A_ClearTarget
		goto Spawn
	
	MissileCheck:
		TNT1 AA 0 A_JumpIfInTargetInventory("IsRed",1,"MissileGo")
		TNT1 A 0 
		goto See
	
	Missile:
		TNT1 A 0 
		TNT1 A 1 A_JumpIfInTargetInventory("IsBlue",1,"See2")
		TNT1 A 1 A_JumpIfCloser(30000,"MissileGo")
		TNT1 A 0 
		goto See2
	
	MissileGo:
		TNT1 A 0 A_JumpIfInTargetInventory("IsMech",1,"MissileGoMech")
		TNT1 A 0 A_JumpIfInventory("ObeliskCharged",1,"MissileCharged")
		TNT1 A 0 A_FaceTarget
		TNT1 A 70 A_PlaySoundEx("tesla/charge","Voice",0)
		TNT1 A 0 A_GiveInventory("ObeliskCharged",15)
		TNT1 A 0 A_CposRefire
		TNT1 A 0 A_PlaySoundEx("tesla/fire","Weapon",0)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ObbyFlareBlue")
		TNT1 A 0 A_CustomMissile("ObeliskShotBlueInfantry",12,0)
		TNT1 A 70 A_FaceTarget
		TNT1 A 0 A_TakeInventory("ObeliskCharged",15)
		goto See
	
	MissileCharged:
		TNT1 A 15
		TNT1 A 0 A_PlaySoundEx("tesla/fire","Weapon",0)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("ObeliskShotBlueInfantry",12,0)
		TNT1 A 70 A_FaceTarget
		TNT1 A 0 A_TakeInventory("ObeliskCharged",15)
		goto See
	
	MissileGoMech:
		TNT1 A 0 A_JumpIfInventory("ObeliskCharged",1,"MissileChargedMech")
		TNT1 A 0 A_FaceTarget
		TNT1 A 70 A_PlaySoundEx("tesla/charge","Voice",0)
		TNT1 A 0 A_GiveInventory("ObeliskCharged",15)
		TNT1 A 0 A_CposRefire
		TNT1 A 0 A_PlaySoundEx("tesla/fire","Weapon",0)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ObbyFlareBlue")
		TNT1 A 0 A_CustomMissile("ObeliskShotBlue",12,0)
		TNT1 A 70 A_FaceTarget
		TNT1 A 0 A_TakeInventory("ObeliskCharged",15)
		goto See
	
	MissileChargedMech:
		TNT1 A 15
		TNT1 A 0 A_PlaySoundEx("tesla/fire","Weapon",0)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_CustomMissile("ObeliskShotBlue",12,0)
		TNT1 A 70 A_FaceTarget
		TNT1 A 0 A_TakeInventory("ObeliskCharged",15)
		goto See

	Death:
		TNT1 A 35 A_JumpIfInventory( "PleaseRespawn", 1, "Respawn" )
		wait

	Respawn:
		TNT1 A -1 A_Respawn
	}
}

Actor ObbyFlare {
	RenderStyle Add
	+NOINTERACTION
	+CLIENTSIDEONLY
	Scale 2.0
	States {
	Spawn:
		CACH B 8 BRIGHT
		CACH B 1 BRIGHT A_FadeOut (0.05)
		Wait
	}
}

Actor ObbyFlareBlue : ObbyFlare {
	States {
	Spawn:
		PPCX A 8 BRIGHT
		PPCX A 1 BRIGHT A_FadeOut (0.05)
		Wait
	}
}

actor ObeliskCharged : Inventory {
	inventory.maxamount 15
}

actor ObeliskShotRed : FastProjectile {
	PROJECTILE
	DONTHURTSHOOTER
	-RANDOMIZE
	+NOEXTREMEDEATH
	+THRUGHOST
	Radius 13
	Height 8
	Speed 2000
	Damage (250)
	DamageType "Explosion"
	Translation "192:207=169:189", "240:246=45:47", "112:127=176:191"
	
	States {
	Spawn:
		TNT1 A -1
		stop
	Death:
		TNT1 A 0 ACS_ExecuteAlways (822, 0)
	CommonDeath:
		TNT1 A 0 A_SpawnItemEx ("PlasmaFlameCluster", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
		TNT1 A 3 bright A_Explode (250, 256, 0)
		stop
	}
}

actor ObeliskShotRedInfantry : ObeliskShotRed {
	Damage (40)
	DamageType "Superweapon"
	States {
	Spawn:
		TNT1 A -1
		stop
	Death:
		TNT1 A 0 ACS_ExecuteAlways (822, 0)
	CommonDeath:
		TNT1 A 0 A_SpawnItemEx( "PlasmaFlameCluster", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION )
		TNT1 A 3 bright A_Explode (50, 256, 0)
		stop
	}
}

actor ObeliskShotBlue : ObeliskShotRed {
	Translation "112:120=200:207"
	
	States {
	Death:
		TNT1 A 0 ACS_ExecuteAlways (822, 0, 1)
		goto CommonDeath
	}
}

actor ObeliskShotBlueInfantry : ObeliskShotRedInfantry {
	Translation "112:120=200:207"
	
	States {
	Death:
		TNT1 A 0 ACS_ExecuteAlways (822, 0, 1)
		goto CommonDeath
	}
}

actor ObeliskTrail {
	+CLIENTSIDEONLY
	+NOINTERACTION
	RenderStyle Add
	Alpha 0.7
	Scale 0.6
	
	States {
	Spawn:
		CACH B 1 BRIGHT A_FadeOut (0.1)
		wait
	}
}

actor ObeliskTrailBlue : ObeliskTrail {
	Scale 1.2
	States {
	Spawn:
		VOLX ABCDEF 1 BRIGHT A_FadeOut (0.1)
		stop
	}
}

// ------------------------
// Obelisk decorations
actor ObeliskVoltageDecorSpawner 29010 {
	//$Category Effects
	tag "Obelisk voltage spawner (square)"
	+NOINTERACTION
	+NOBLOCKMAP
	+SHOOTABLE
	health 999999
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 60 A_SpawnItemEx ("ObeliskVoltageDecor")
		loop
	Death:
		TNT1 A 0
		stop
	}
}

actor ObeliskVoltageDecor {
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	reactiontime 35
	states {
	Spawn:
		TNT1 AAAAAA 1 A_SpawnItemEx ("ObeliskVoltageDecorFX", random (-96,96), random (-96,96), -224, 0, 0, 25)
		TNT1 A 0 A_Countdown
		loop
	Death:
		TNT1 A 0
		stop
	}
}

actor ObeliskVoltageDecorFX {
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	radius 1
	height 1
	renderstyle add
	alpha 1.0
	scale 0.75
	states {
	Spawn:
		VOLX ABCDEFEDCB 2 BRIGHT A_FadeOut(0.1)
		stop
	}
}

actor ObeliskVoltageDecorSpawnerRound 29013 {
	//$Category Effects
	tag "Obelisk voltage spawner (round)"
	+NOINTERACTION
	+NOBLOCKMAP
	+SHOOTABLE
	health 999999
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 60 A_SpawnItemEx ("ObeliskVoltageDecorRound")
		loop
	Death:
		TNT1 A 0
		stop
	}
}

actor ObeliskVoltageDecorRound {
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	reactiontime 14
	states {
	Spawn:
		TNT1 AAAAA 1 A_SpawnItemEx ("ObeliskVoltageDecorFX", random (0, 160), 0, -224, 0, 0, 25, random (0, 360))
		TNT1 A 0 A_Countdown
		loop
	Death:
		TNT1 A 0
		stop
	}
}

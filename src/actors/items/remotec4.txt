// REMOTE C4
// TODO: make the weapon sprite include the detonator!

actor RemoteC4Ammo : Ammo {
	inventory.amount 0
	inventory.maxamount 5
	ammo.backpackamount 1
	ammo.backpackmaxamount 5
}

actor RemoteC4 : AOWWeapon {
	+NOALERT
	obituary "%o bonks %k with a 100kg Remote C4."
	weapon.ammotype "RemoteC4Ammo"
	weapon.ammouse 1
	weapon.ammogive 5
	states {
	Ready:
		RC4G B 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "ReadyFull")
		RC4G B 5 A_WeaponReady (WRF_NOPRIMARY)
		loop
	ReadyFull:
		RC4G A -1 A_WeaponReady
		wait
	Deselect:
		RC4G B 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "DeselectFull")
		RC4G B 0 A_Lower
		RC4G B 1 A_Lower
		goto Deselect+1
	DeselectFull:
		RC4G A 0 A_Lower
		RC4G A 1 A_Lower
		loop
	Select:
		RC4G B 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "SelectFull")
		RC4G B 1 A_Raise
		RC4G B 0 A_Raise
		loop
	SelectFull:
		RC4G A 0 A_TakeInventory ("RemoteC4Detonator", 1)
		RC4G A 1 A_Raise
		RC4G A 0 A_Raise
		goto SelectFull+1
	Fire:
		TNT1 A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "ReadyFull")
		RC4G A 1 offset (1,31)
		RC4G A 1 offset (7,37)
		RC4G A 1 offset (13,43)
		RC4G A 1 offset (19,49)
		RC4G A 1 offset (26,56)
		RC4G A 1 offset (34,64)
		RC4G A 1 offset (42,72)
		RC4G A 1 offset (54,84) A_PlaySound ("c4/remote", CHAN_WEAPON)
		RC4G A 1 offset (60,90)
		RC4G A 1 offset (40,70)
		RC4G B 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "ReadyFull")
		RC4G B 0 A_JumpIfInventory ("IsBlue", 1, "FireBlue")
		RC4G B 5 A_FireCustomMissile ("RemoteC4Thrown")
		RC4G B 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "FireEndFull")
		goto FireEnd
	FireBlue:
		TNT1 A 5 A_FireCustomMissile ("RemoteC4ThrownBlue")
		TNT1 A 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "FireEndFull")
		goto FireEnd
	FireEnd:
		RC4G B 1 offset (30,60)
		RC4G B 1 offset (10,80)
		RC4G B 1 offset (0,100)
		RC4G B 1 offset (0,110)
		goto Ready
	FireEndFull:
		RC4G B 1 offset(30,60)
		RC4G B 1 offset(10,40)
		RC4G B 1 offset(0,40)
		RC4G A 1 offset(0,60)
		RC4G A 1 offset(0,80)
		RC4G A 1 offset(0,100)
		RC4G A 1 offset(0,110)
		goto Ready
	AltFire:
		RC4E A 0 A_JumpIfInventory ("RemoteC4Ammo", 1, "AltFireFull")
		RC4E ABCDEFGH 1
		RC4E I 2
		RC4E J 2 A_PlayWeaponSound ("c4/remote2")
		RC4E I 4 A_GiveInventory ("C4DetonateCheck", 1)
		RC4E H 6 A_TakeInventory ("C4DetonateCheck", 666)
		RC4E GFEDCBA 1
		goto Ready
	AltFireFull:
		RC4D ABCDEFGH 1
		RC4D I 2
		RC4D J 2 A_PlayWeaponSound ("c4/remote2")
		RC4D I 5 A_GiveInventory ("C4DetonateCheck", 1)
		RC4D H 30 A_TakeInventory ("C4DetonateCheck", 666)
		RC4D GFEDCBA 1
		goto ReadyFull
	}
}

actor RemoteC4Thrown {
	PROJECTILE
	+RANDOMIZE
	+NOEXTREMEDEATH
	-NOGRAVITY
	+NOTARGETSWITCH
	Obituary "%o was somehow knocked out by %k's flying Remote C4 bomb."
	Radius 6
	Height 8
	Speed 30
	Damage 0
	Gravity 0.5
	Scale 0.2
	Health 250
	DamageType "Explosion"
	states {
	Spawn:
		RC4T A -1
		loop
	Death:
		RC4T A 0
		RC4T A 0 A_SpawnItem ("RemoteC4Armed")
		TNT1 A 4 bright 
		stop
	}
}

actor RemoteC4ThrownBlue : RemoteC4Thrown {
	damagetype "Explosion"
	translation "16:47=192:207", "168:191=192:207"
	states {
	Death:
		RC4T A 0
		RC4T A 0 A_SpawnItem ("RemoteC4ArmedBlue")
		TNT1 A 4 bright 
		stop
	}
}

actor RemoteC4Armed {
	+NOEXTREMEDEATH
	+NOGRAVITY
	+NOBLOOD
	+SHOOTABLE
	+NOTARGETSWITCH
	-SOLID
	scale 0.2
	health 20
	radius 6
	height 6
	mass 0x7FFFFFFF
	speed 0
	damage 0
	damagetype "Explosion"
	obituary "%o went boom from %k's Remote C4!"
	
	DamageFactor "Explosion",        0.0
	DamageFactor "Fire",             0.0
	DamageFactor "Chemical",         0.0
	DamageFactor "RazorWire",        0.0
	DamageFactor "Time",             0.0
	DamageFactor "Superweapon",      0.0
	DamageFactor "MassDriver",       0.0
	DamageFactor "AntiArmor",        0.0
	DamageFactor "FireSpecial",      0.0
	DamageFactor "ChemicalSpecial",  0.0
	DamageFactor "Mechstomp",        0.0
	DamageFactor "Monster",          0.0
	
	states {
	Spawn:
		RC4T A 0
		RC4T A 0 A_GiveInventory ("IsRed", 1)
		goto Idle
	
	Idle:
		RC4T A 0 A_JumpIfInTargetInventory ("RemoteC4Ammo", 5, "Death.Disarm")
		RC4T A 1 A_JumpIfInTargetInventory ("C4DetonateCheck", 1, "Death")
		RC4T A 3
		loop
	
	Death:
		RC4T A 0 A_NoGravity
		RC4T A 10 A_PlaySound ("c4/detonate")
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("ExplosionMainLarge")
		TNT1 A 0 A_PlaySound ("weapons/rocklx")
		TNT1 A 0 A_PlayWeaponSound ("weapons/rocklx")
		TNT1 A 0 ACS_ExecuteAlways (978, 0, 1)
		TNT1 A 0 bright A_Explode(250,640)
		TNT1 A 0 bright A_Explode(500,64)
		TNT1 A 0 bright A_Explode(400,196)
		TNT1 A 0 bright A_Explode(500,256)
		goto DeathFinish
	
	DeathFinish:
		TNT1 A 0 A_SpawnItemEx("FlameWallDecay", random(-48,48), random(-48,48), 4, 0, 0, 0, 0, 0, 160)
		TNT1 A 5
		stop
	
	Death.Disarm:
		RC4T A 35 A_PlaySound ("c4/disarm")
		RC4T AAAAAAAAAA 1 A_FadeOut (0.05)
		stop
	}
}

actor RemoteC4ArmedBlue : RemoteC4Armed {
	Translation "16:47=196:207", "168:191=196:207"
	States {
	Spawn:
		RC4T A 0
		RC4T A 0 A_GiveInventory ("IsBlue", 1)
		goto Idle
	
	DeathFinish:
		TNT1 A 0 A_SpawnItemEx("FlameWallDecayBlue", random(-48,48), random(-48,48), 4, 0, 0, 0, 0, 0, 160)
		TNT1 A 5
		stop
	}
}

actor C4DetonateCheck : Boolean {}
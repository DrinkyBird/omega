// Missile Launcher
actor MissileLauncher : AOWWeapon {
	+NOAUTOFIRE
	Inventory.PickupMessage "You got the missile laucher!"
	Obituary "$OB_MISSILELAUNCHER"
	Weapon.SelectionOrder 13
	Weapon.AmmoType "MissileAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 2
	Weapon.PreferredSkin "MarineRockets"

	States {
	Select:
		MSLR A 0 ACS_ExecuteAlways (963, 0) // init targeter
		MSLR A 0 A_JumpIfInventory ("IsBlue", 1, "SelectBlue")
		MSLR A 1 A_Raise
		wait

	SelectBlue:
		MSLB A 1 A_Raise
		wait

	Deselect:
		MSLR A 0 A_JumpIfInventory ("IsBlue", 1, "DeselectBlue")
		MSLR A 1 A_Lower
		loop

	DeselectBlue:
		MSLB A 1 A_Lower
		loop

	Ready:
		MSLR A 0 A_JumpIfInventory ("IsBlue", 1, "ReadyBlue")
		MSLR A 1 A_WeaponReady (WRF_NOSECONDARY)
		wait

	ReadyBlue:
		MSLB A 1 A_WeaponReady (WRF_NOSECONDARY)
		wait

	Fire:
		MSLB A 0
		MSLB A 0 A_JumpIfInventory ("IsInSpawnRoom",1,"Ready")
		MSLB A 0 A_GunFlash
		MSLB A 0 A_PlaySound ("missilelauncher/fire", CHAN_WEAPON)
		MSLB A 0 A_JumpIfTargetInLOS ("Fire.Homing")
		MSLB A 0 A_FireCustomMissile ("HeavyMissile")
		goto AnimationRed

	Fire.Homing:
		MSLB A 0 A_FireCustomMissile ("HeavyHomingMissile")
		goto AnimationRed

	AnimationRed:
		MSLB A 0 A_JumpIfInventory ("IsBlue", 1, "AnimationBlue")
		MSLR A 1 offset (1, 35)
		MSLR A 1 offset (2, 38)
		MSLR A 1 offset (3, 41) A_Recoil (18)
		MSLR A 1 offset (4, 44)
		MSLR A 1 offset (5, 47)
		MSLR A 1 offset (6, 50)
		MSLR A 1 offset (5, 47)
		MSLR A 1 offset (4, 44)
		MSLR A 1 offset (3, 41)
		MSLR A 1 offset (2, 38)
		MSLR A 1 offset (1, 35)
		MSLR A 1 offset (0, 32)
		MSLR A 38 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		MSLR A 0 A_CheckReload
		MSLR A 0 A_PlayWeaponSound ("missilelauncher/reload")
		MSLR BC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		MSLR D 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		MSLR E 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		MSLR E 0 A_ReFire
		goto Ready

	AnimationBlue:
		MSLB A 1 offset (1, 35)
		MSLB A 1 offset (2, 38)
		MSLB A 1 offset (3, 41) A_Recoil (18)
		MSLB A 1 offset (4, 44)
		MSLB A 1 offset (5, 47)
		MSLB A 1 offset (6, 50)
		MSLB A 1 offset (5, 47)
		MSLB A 1 offset (4, 44)
		MSLB A 1 offset (3, 41)
		MSLB A 1 offset (2, 38)
		MSLB A 1 offset (1, 35)
		MSLB A 1 offset (0, 32)
		MSLB A 38 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		MSLR A 0 A_CheckReload
		MSLB A 0 A_PlayWeaponSound ("missilelauncher/reload")
		MSLB BC 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		MSLB D 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		MSLB E 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		MSLB E 0 A_ReFire
		goto Ready

	Flash:
		MSFR A 2 bright A_Light1
		MSFR B 2 bright 
		MSFR C 2 bright A_Light2
		MSFR D 2 bright
		MSFR D 0 A_Light0
		stop

	Spawn:
		LAUN A -1
		stop
	}
}

actor MissileAmmo : Ammo {
	+IGNORESKILL
	inventory.amount 15
	inventory.maxamount 15
	ammo.backpackamount 15
	ammo.backpackmaxamount 15
}

actor HeavyMissile {
	PROJECTILE
	-RANDOMIZE
	Radius 11
	Height 8
	Speed 38
	Damage (90)
	Obituary "$OB_MISSILELAUNCHER"
	DeathSound "weapons/rocklx"
	DamageType "Explosion"
	Decal "Scorch"

	States {
	Spawn:
		MISR A 2 bright
		MISR A 0 bright A_PlaySound ("ambient/rocket", CHAN_BODY, 1.0, true)
	Idle:
		MISR A 0 bright A_ScaleVelocity (1.03)
		MISR AAAAA 1 bright A_SpawnItemEx ("HeavyMissileSmoke", -16)
		loop

	Death:
		TNT1 A 0 A_SpawnItemEx ("FlameDecay", random(-16,16), random(-16,16), 4, 0, 0, 0, 0, 0, 160)
		TNT1 A 0 A_StopSound (CHAN_BODY)
		TNT1 A 0 A_Explode (160, 320)
		TNT1 A 0 A_SpawnItemEx ("HeavyMissileQuake")
		TNT1 A 5 A_SpawnItemEx ("ExplosionMain")
		stop
	}
}

actor HeavyHomingMissile : HeavyMissile {
	+SEEKERMISSILE
	Obituary "$OB_SEEKERMISSILE"
	Speed 26

	States {
	Spawn:
		MISR A 2 bright
		MISR A 0 bright A_PlaySound ("seeker/beep", CHAN_ITEM, 1.0, true)
		MISR A 0 bright A_PlaySound ("ambient/rocket", CHAN_BODY, 1.0, true)
	Idle:
		MISR A 0 A_SpawnItemEx ("HeavyMissileSmoke", -16)
		MISR A 2 bright A_SeekerMissile (4, 10)
		loop
	Death:
		TNT1 A 0 A_StopSound (CHAN_ITEM)
		goto Super::Death
	}
}

actor HeavyMissileQuake {
	+CLIENTSIDEONLY
	+NOINTERACTION
	states {
	Spawn:
		TNT1 A 0
		TNT1 A 5 Radius_Quake (5, 5, 0, 7, 0)
		TNT1 A 4 Radius_Quake (4, 4, 0, 7, 0)
		TNT1 A 3 Radius_Quake (3, 3, 0, 7, 0)
		TNT1 A 2 Radius_Quake (2, 2, 0, 7, 0)
		TNT1 A 1 Radius_Quake (1, 1, 0, 7, 0)
		stop
	}
}

actor HeavyMissileSmoke {
	+CLIENTSIDEONLY
	+NOINTERACTION
	alpha 0.6
	scale 0.2
	states {
	Spawn:
		SMOK A 0
		SMOK A 0 A_Jump (256, "Spawn1", "Spawn2", "Spawn3", "Spawn4")
		goto Spawn1
	Spawn1:
		SMOK A 1 A_FadeOut
		wait
	Spawn2:
		SMOK B 1 A_FadeOut
		wait
	Spawn3:
		SMOK C 1 A_FadeOut
		wait
	Spawn4:
		SMOK D 1 A_FadeOut
		wait
	}
}
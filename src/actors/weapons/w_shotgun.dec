// Shotgun
actor Boomstick : AOWWeapon replaces Shotgun 21080 {
	//$Category Weapons
	Tag "Shotgun"
	Obituary "$OB_SHOTGUN"
	Inventory.PickupMessage "You got the shotgun!"
	Weapon.SelectionOrder 17
	Weapon.KickBack 80
	Weapon.AmmoType "ShotgunAmmo"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 5
	Weapon.PreferredSkin "MarineShotgun"
	States {
	Ready:
		SHTG A 1 A_WeaponReady
		wait
	Deselect:
		SHTG A 1 A_Lower
		wait
	Select:
		SHTG A 1 A_Raise
		wait
	Fire:
		SHTG A 0 Bright A_PlaySound ("weapons/shotgf", CHAN_WEAPON)
		SHTG A 0 Bright A_GunFlash
		SHTG A 0 Bright Radius_Quake (3, random (3, 5), 0, 1, 0)
		SHTG A 0 Bright A_FireBullets (5.5, 5.5, 2, 12, "HandgunPuff", FBF_NORANDOM, 350)
		SHTG A 1 Bright A_FireBullets (5.5, 5.5, 6, 8, "HandgunPuff", FBF_USEAMMO|FBF_NORANDOM, 1000)
		SHTG A 1 Bright Offset (0, 41)
		SHTG A 1 Bright Offset (0, 45)
		SHTG A 1 Bright Offset (0, 49)
		SHTG A 1 Offset (0, 45)
		SHTG A 1 Offset (0, 41)
		SHTG A 1 Offset (0, 38)
		SHTG A 1 Offset (0, 35)
		SHTG B 1 Offset (-24, 31)
		SHTG B 1 Offset (-42, 30)
		SHTG B 1 Offset (-56, 29)
		SHTG C 1 Offset (-60, 31) // A_PlaySound ("shotgun/pump", 5)
		SHTG C 2 Offset (-84, 32)
		SHTG C 1 Offset (-84, 36)
		SHTG D 1 Offset (-84, 40)
		SHTG D 3 Offset (-84, 46) A_SpawnItemEx ("ShotgunShellCasing", 12, 9, 33,
			random (-2, -4), random (2, 3), random (1, 2), random (45, 60))
		SHTG D 1 Offset (-84, 40)
		SHTG D 1 Offset (-84, 36)
		SHTG C 2 Offset (-84, 32)
		SHTG C 1 Offset (-70, 31)
		SHTG C 1 Offset (-56, 30)
		SHTG B 1 Offset (-42, 29)
		SHTG B 1 Offset (-28, 30)
		SHTG B 1 Offset (-14, 31)
		SHTG A 4 Offset ( -1, 32)
		SHTG A 0 A_ReFire
		goto Ready
	Flash:
		SHTF A 3 Bright A_Light1
		SHTF B 3 Bright A_Light2
		SHTF B 0 A_Light0
		stop
	Spawn:
		SHOT A -1
		stop
	}
}

actor ShotgunAmmo : Ammo {
	inventory.amount 5
	inventory.maxamount 50
	ammo.backpackamount 5
	ammo.backpackmaxamount 50
}
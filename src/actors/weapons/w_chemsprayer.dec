// Chemical sprayer
actor ChemicalSprayer : AOWWeapon 21066 {
	+ALT_AMMO_OPTIONAL
	inventory.pickupmessage "You got the chemical sprayer!"
	attacksound "Chem/fire"
	weapon.selectionorder 9
	weapon.kickback 1
	weapon.ammotype "ChemicalAmmo"
	weapon.ammouse 1
	weapon.ammogive 25
	weapon.ammotype2 "ChemicalGrenadeAmmo"
	weapon.ammouse2 1
	weapon.ammogive2 0
	Weapon.PreferredSkin "MarinePlasma"

	States {
	Spawn:
		CHMG X -1
		stop

	Ready:
		CHMS A 0 A_StopSound (CHAN_WEAPON)
		CHMS A 1 A_WeaponReady
		wait

	Deselect:
		CHMS A 0 A_StopSound (CHAN_WEAPON)
		CHMS A 1 A_Lower
		wait

	Select:
		CHMS A 1 A_Raise
		loop

	Fire:
		TNT1 A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "Ready")
		CHMS A 0 A_PlaySound ("chem/fire", CHAN_WEAPON, 1.0, 1)
		CHMS A 0 A_GunFlash
		CHMS A 1 bright offset (0, 35) A_FireCustomMissile ("ChemicalShot",random(-2,2),0,8,random(-2,2))
		CHMS A 1 bright offset (2, 36) A_FireCustomMissile ("ChemicalShotDummy",random(-2,2),0,8,random(-2,2))
		CHMS A 1 bright offset (3, 37) A_FireCustomMissile ("ChemicalShot",random(-2,2),1,8,random(-2,2))
		CHMS A 1 bright offset (1, 36) A_FireCustomMissile ("ChemicalShotDummy",random(-2,2),0,8,random(-2,2))
		CHMS A 8 bright offset (-1, 34) A_ReFire
		CHMS A 0 A_StopSound (CHAN_WEAPON)
		goto Ready

	AltFire:
		CHMS A 0 A_JumpIfNoAmmo ("AltRejected")
		TNT1 A 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "Ready")
		CHMS A 19
		CHMS B 1 A_PlayWeaponSound ("grenade/fire")
		CHMS C 1
		CHMS D 1 bright A_FireCustomMissile ("ChemicalGrenade", 0, 1, 8, 0)
		CHMS EFGH 1 bright
		CHMS A 9
		CHMS A 0 A_ReFire
		CHMS A 15 A_WeaponReady // This is to prevent the altfire sound from cutting off
		goto Ready

	AltRejected:
		CHMS A 35 A_WeaponReady (WRF_NOSECONDARY)
		goto Ready

	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		TNT1 A 0 A_Light0
		stop
	}
}

// =============================================================================
actor ChemicalAmmo : Ammo {
	inventory.amount 50
	inventory.maxamount 200
	ammo.backpackamount 50
	ammo.backpackmaxamount 200
	states {
	Spawn:
		TNT1 A 0
		stop
	}
}

actor ChemicalGrenadeAmmo : Ammo {
	inventory.amount 1
	inventory.maxamount 4
	ammo.backpackamount 1
	ammo.backpackmaxamount 4
	states {
	Spawn:
		TNT1 A 0
		stop
	}
}

// =============================================================================
actor ChemicalShot {
	PROJECTILE
	+FORCEXYBILLBOARD
	obituary "$OB_CHEMSPRAYER"
	renderstyle Add
	alpha 0.9
	Radius 8
	Height 8
	Speed 25
	Damage (4)
	DamageType "Chemical"
	Scale 0.5

	States {
	Spawn:
		TNT1 A 4
		TIBX ABCDEFGHIJK 2 bright

	Clear:
		TNT1 A 0 A_SpawnItemEx("AnthraxDecay", random(-16,16), random(-16,16), 4, 0, 0, 0, 0, 0, 240)
		TIBX L 2 bright
		TIBX MMNNOOPPQQ 1 bright A_FadeOut(0.1)
		stop

	Death:
		TNT1 A 1 //A_Explode (5, 140)
		TNT1 A 0 A_SpawnItemEx("AnthraxDecay", random(-16,16), random(-16,16), 4, 0, 0, 0, 0, 0, 240)
		TIBX H 3 bright A_SpawnItemEx("ChemFakeDeath",0,0,0,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM)
		//^ Dat is optimization sir. Instead of 4 Kb/s out when shooting MCT, it's about 2.3 Kb/s.
		stop
	}
}

actor ChemicalShotDummy {
	PROJECTILE
	+FORCEXYBILLBOARD
	obituary "$OB_CHEMSPRAYER"
	renderstyle Add
	alpha 0.9
	Radius 8
	Height 8
	Speed 25
	Damage (0)
	DamageType "Chemical"
	Scale 0.5

	States {
	Spawn:
		TNT1 A 4
		TIBX ABCDEFGHIJK 2 bright

	Clear:
		TIBX L 2 bright
		TIBX MMNNOOPPQQ 1 bright A_FadeOut(0.1)
		stop

	Death:
		TNT1 A 1
		TIBX H 3 bright A_SpawnItemEx("ChemFakeDeath",0,0,0,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM)
		//^ Dat is optimization sir. Instead of 4 Kb/s out when shooting MCT, it's about 2.3 Kb/s.
		stop
	}
}

actor ChemFakeDeath : ChemicalShot {
	Damage (0)
	Alpha 0.375
	+CLIENTSIDEONLY // This guy decreases network usage, remember? :(

	States {
	Death:
	Spawn:
		TIBX IIJJKKLLMMOOPPQQ 1 bright A_FadeOut(0.035)
		stop
	}
}

// =============================================================================
actor AnthraxDecay {
	+NOBLOCKMAP
	+LOWGRAVITY
	+NODAMAGETHRUST
	DamageType "Chemical"
	Obituary "$OB_CHEMSPRAYER"
	ReactionTime 17

	States {
	Spawn:
		TNT1 AA 0 A_PlaySound("chem/chem")
		goto Idle

	Idle:
		TNT1 A 0
		TNT1 AAA 5 A_SpawnItemEx("ChemicalFlame", random(-16,16), random(-16,16), random(8,16), 0, 0, random(1,3), 0, 128, 32)
		TNT1 A 0 A_Explode (4, 128)
		TNT1 A 0 A_Countdown
		loop
	}
}

actor AnthraxDecaySpecial : AnthraxDecay {
	DamageType "ChemicalSpecial" // neutral
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */
actor ChemicalGrenade {
	PROJECTILE
	-RANDOMIZE
	-NOGRAVITY
	+LOWGRAVITY
	Radius 11
	Height 8
	Speed 35
	Damage (30)
	SeeSound ""
	DeathSound "weapons/rocklx"
	Obituary "$OB_CHEMSPRAYER_GRENADE"
	DamageType "Chemical"
	Decal "ChemScorchStrong"
	Translation "80:111=120:127"

	States {
	Spawn:
		SGRN A 2 bright A_SpawnItemEx ("PulseBallSmokePuff", -25)
		loop

	Death:
		TNT1 A 0 bright A_SpawnItem ("ChemicalExplosion")
		TNT1 A 0 bright A_Gravity
		TNT1 A 0 bright A_Explode (90, 128, 1)
		TNT1 AAAAAAAAA 70 A_SpawnItemEx("AnthraxDecay", random(-64,64), random(-64,64), 4)
		TNT1 A 0
		stop
	}
}
// Razor wire
actor Utility_RazorWire {
	+SHOOTABLE
	+NOBLOOD
	+NODAMAGETHRUST
	Health 400
	Speed 0
	Radius 32
	Height 64
	Mass 0x7FFFFFFF
	Scale 0.25
	RenderStyle translucent
	Alpha 0.0
	Obituary "%o walked into razor wire."
	DamageFactor "Disarm", 0.0
	DamageFactor "Mechstomp", 0.0

	States {
	Spawn:
		WIRE AA 0 Thing_SetSpecial (0, 84, 345, 0, TEAM_RED)
		WIRE AAAAAAAAAA 3 A_FadeIn (0.1)
		WIRE A 0 A_ChangeFlag ("NOCLIP", 0)
		WIRE A 35 A_Gravity
		WIRE A 12 ACS_ExecuteAlways( SC_RazorWire, 0, TEAM_RED )
		wait

	Death:  
		TNT1 A 0 
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("ExplosionMain", 0, 0, 32)
		TNT1 A 0 A_PlaySound ("weapons/rocklx")
		TNT1 A 4
		stop

	Death.Deconstruction:
		"----" A 0 A_NoBlocking
		"----" A 2 A_PlaySound ("utility/deconstruct")
		"----" AAAAAAAAAA 2 A_Fadeout(0.1) 
		stop
	}
}

actor Utility_RazorWireBlue : Utility_RazorWire {
	States {
	Spawn:
		WIRE AA 0 Thing_SetSpecial (0, 84, 345, 0, TEAM_BLUE)
		WIRE AAAAAAAAAA 3 A_FadeIn (0.1)
		WIRE A 0 A_ChangeFlag ("NOCLIP", 0)
		WIRE A 35 A_Gravity
		WIRE A 12 ACS_ExecuteAlways( SC_RazorWire, 0, TEAM_BLUE )
		wait
	}
}

actor PowerRazorWire : PowerSpeed {
	speed 0.5
}

actor RazorWireSlow : PowerupGiver {
	+AUTOACTIVATE
	Inventory.MaxAmount 0
	Powerup.Type "RazorWire"
	Powerup.Duration 35
}
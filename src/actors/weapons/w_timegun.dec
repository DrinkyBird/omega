// TIME GUN
Actor TimeGun : AOWWeapon
{
	+WEAPON.NOAUTOFIRE
	+BLOODLESSIMPACT
	DropItem				"NewBackpack"
	Obituary				"%o was sucked in a time-space continuum vortex."
	Inventory.PickupSound	"Weapons/Pickup"
	Weapon.AmmoGive			100
	Weapon.AmmoType			"TimeGunAmmo"
	Weapon.AmmoUse			0
	Weapon.Kickback			25
	Weapon.PreferredSkin	"MarineTimegun"
	AttackSound				"mech/gauss"

	States
	{
		Spawn:
			TNT1 A 0
			stop

		Ready:
			TMGN A 10 A_WeaponReady
			TMGN A 0 A_GiveInventory ("TimeGunAmmo", 2)
			loop

		Deselect:
			TMGN A 1 A_Lower
			wait

		Select:
			TMGN A 0
			TMGN A 0 ACS_ExecuteAlways (959, 0)
		SelectLoop:
			TMGN A 1 A_Raise
			wait

		Fire:
			TMGN A 0 A_JumpIfInventory ("PowerTimeGunSpeed", 1, "HyperspeedLoop")
			TMGN A 0 A_JumpIfInventory ("TimeGunRotation", 2, "Fire.Hyperspeed")
			TMGN A 0 A_JumpIfInventory ("TimeGunRotation", 1, "Fire.Float")
		Fire.Boost:
			TMGN A 0 A_JumpIfInventory ("TimeGunAmmo", 25, 1)
			goto OutOfAmmo
			TMGN B 0 A_TakeInventory ("TimeGunAmmo", 25)
			TMGN B 0 bright A_JumpIfInventory ("IsBlue", 1, "Fire.BoostBlue")
			TMGN B 0 A_FireBullets (0, 0, 1, 0, "BoostPuffRed", 0)
			TMGN D 0 ACS_ExecuteAlways (958, 0, 2) // reward
			goto Fire.Cooldown

		Fire.BoostBlue:
			TMGN B 0 A_FireBullets(0, 0, 1, 0, "BoostPuffBlue", 0)
			TMGN D 0 ACS_ExecuteAlways (958, 0, 2)
			goto Fire.Cooldown

		Fire.Float:
			TMGN C 0 A_JumpIfInventory ("TimeGunAmmo", 25, 1)
			goto OutOfAmmo
			TMGN D 0 A_TakeInventory ("TimeGunAmmo", 25)
			TMGN D 0 A_JumpIfInventory ("IsBlue", 1, "Fire.FloatBlue")
			TMGN D 0 A_FireBullets(0, 0, 1, 0, "FloatPuffRed", 0)
			TMGN D 0 ACS_ExecuteAlways (958, 0, 2)
			goto Fire.Cooldown


		Fire.FloatBlue:
			TMGN D 0 A_FireBullets (0, 0, 1, 0, "FloatPuffBlue", 0)
			TMGN D 0 ACS_ExecuteAlways (958, 0, 2)
			goto Fire.Cooldown

		Fire.Cooldown:
			TMGN D 0 A_PlayWeaponSound ("mech/gauss")
			TMGN D 0 A_ChangeFlag ("PICKUP", 0)
			TMGN D 2 Bright A_RailAttack (0, 2, 1, "FF 96 00","FF D4 00", 0, 0, "GoldenPuff")
			TMGN CEFA 1
			TMGN A 0 A_ChangeFlag ("PICKUP", 1)
			TMGN A 25 A_PlaySound ("80mm/reload", CHAN_WEAPON)
			goto Ready

		Fire.Hyperspeed:
			TMGN G 16 BRIGHT A_PlayWeaponSound ("plasmacannon/fire")
			TMGN H 12
			TMGN H 0 A_PlaySound ("misc/timewarp", CHAN_WEAPON)
			TMGN H 0 A_SetTranslucent (0.5, 1)
			TMGN H 0 A_GiveInventory ("TimeGunSpeed", 1)
		HyperspeedLoop:
			TMGN H 1 bright DamageThing (1)
			TMGN H 0 A_SpawnItemEx ("TimeGunFlare", random(-64,64), random(-64,64), random(0,128), 0, 0, 1)
			TMGN H 0 bright A_ReFire
		HyperspeedEnd:
			TMGN H 0 A_TakeInventory ("PowerTimeGunSpeed", 1)
			TMGN H 0 A_GiveInventory ("GoldHue", 1)
			TMGN H 0 A_SpawnItemEx ("TimeGunFlareExplosion")
			TMGN A 0 A_SetTranslucent (1.0, 0)
			TMGN A 0 SetPlayerProperty (0, 0, 0)
			TMGN A 35 BRIGHT A_PlaySound ("misc/timeunwarp", CHAN_WEAPON)
			goto Ready

		Nope:
			TMGN A 35 SetPlayerProperty (0, 0, 0)
			goto Ready

		OutOfAmmo:
			TMGN A 2 A_PlaySound ("Plasma/FailFire", 6)
			TMGN A 0 A_PlaySound ("Plasma/Vapour", 7)
			TMGN A 0 A_TakeInventory ("TimeGunCharged", 1)
			TMGN AAAAAA 2 A_FireCustomMissile("OverheatSmokeSpawner",0,0,6,-2)
			TMGN A 35
			goto Ready

		AltFire:
			TMGN A 0 ACS_Execute (958, 0, 1)
			goto Ready
	}
}

// =============================================================================
//
Actor TimeGunRotation : Inventory {Inventory.MaxAmount 2}
Actor TimeGunCharged : Inventory {}

// =============================================================================
// This was supposed to pop up when the player tried to de-buff a friend..
// perhaps it could be used somewhere?
//
Actor Nope
{
	+CLIENTSIDEONLY
	+NOGRAVITY
	RenderStyle translucent
	Alpha 1.0
	Scale 0.1

	States
	{
		Spawn:
			NOPE A 0
			NOPE A 0 A_PlaySound ("misc/nope")
			NOPE A 0 ThrustThingZ (0, 12, 0, 0)
			NOPE A 3 A_FadeOut (0.1)
			wait
	}
}

// =============================================================================
// Boost power-up
//
Actor PowerTimeGunBoost : PowerSpeed
{
	Speed 1.4
	Powerup.Duration -15
}

// =============================================================================
//
Actor TimeGunBoost : PowerupGiver
{
	+AUTOACTIVATE
	Inventory.MaxAmount 0
	Powerup.Type "TimeGunBoost"
	Inventory.PickupSound ""
	Powerup.Duration -15
	Powerup.Color "FF AA 00", 0.15

	States
	{
		Spawn:
			TNT1 A 0
			stop
	}
}

// =============================================================================
//
Actor TimeGunBoostRed : CustomInventory
{
	Inventory.MaxAmount 0
	Inventory.PickupMessage "`"
	Inventory.PickupSound "misc/p_pkup"

	States
	{
		Spawn:
			TNT1 A 5
			stop

		Pickup:
			TNT1 A 0 A_JumpIfInventory ("IsBlue", 1, "Slow")
			TNT1 A 0 A_JumpIfInventory ("Knife", 1, "Failure") // Not on stealths because this reveals them
			TNT1 A 0 A_JumpIfInventory ("StealthLaserRifle", 1, "Failure")
			TNT1 A 0 ACS_Execute (958, 0, 0, 0) // intel message
			TNT1 A 0 A_GiveInventory ("TimeGunBoost", 1)
			stop

		Slow:
			TNT1 A 0 ACS_Execute (958, 0, 0, 2)
			TNT1 A 0 A_GiveInventory ("TimeGunSlow", 1)
			stop

		Failure:
			TNT1 A 0
			fail
	}
}

Actor TimeGunBoostBlue : TimeGunBoostRed
{
	States
	{
		Pickup:
			TNT1 A 0 A_JumpIfInventory ("IsRed", 1, "Slow")
			TNT1 A 0 A_JumpIfInventory ("Knife", 1, "Failure")
			TNT1 A 0 A_JumpIfInventory ("StealthLaserRifle", 1, "Failure")
			TNT1 A 0 ACS_Execute (958, 0, 0, 0) // intel message
			TNT1 A 0 A_GiveInventory ("TimeGunBoost", 1)
			stop
	}
}

// =============================================================================
// Float power-up
//
Actor PowerTimeGunFloat : PowerFlight {}

// =============================================================================
//
Actor TimeGunFloat : PowerupGiver
{
	+AUTOACTIVATE
	Inventory.MaxAmount		0
	Inventory.PickupSound	""
	Powerup.Type			"TimeGunFloat"
	Powerup.Duration		-20
	Powerup.Color			"FF AA 00", 0.15

	States
	{
		Spawn:
			TNT1 A 0
			stop
	}
}

// =============================================================================
//
Actor TimeGunFloatRed : TimeGunBoostRed
{
	States
	{
		Pickup:
			TNT1 A 0 A_JumpIfInventory ("IsBlue", 1, "Weaken")
			TNT1 A 0 A_JumpIfInventory ("Knife", 1, "Failure")
			TNT1 A 0 A_JumpIfInventory ("StealthLaserRifle", 1, "Failure")
			TNT1 A 0 ACS_Execute (958, 0, 0, 1) // intel message
			TNT1 A 0 A_GiveInventory ("TimeGunFloat", 1)
			stop

		Weaken:
			TNT1 A 0 ACS_Execute (958, 0, 0, 3)
			TNT1 A 0 A_GiveInventory ("TimeGunWeak", 1)
			stop
	}
}

// =============================================================================
//
Actor TimeGunFloatBlue : TimeGunFloatRed
{
	States
	{
		Pickup:
			TNT1 A 0 A_JumpIfInventory ("IsRed", 1, "Weaken")
			TNT1 A 0 A_JumpIfInventory ("Knife", 1, "Failure")
			TNT1 A 0 A_JumpIfInventory ("StealthLaserRifle", 1, "Failure")
			TNT1 A 0 ACS_Execute (958, 0, 0, 1) // intel message
			TNT1 A 0 A_GiveInventory ("TimeGunFloat", 1)
			stop
	}
}

// =============================================================================
// Slow debuff
//
Actor PowerTimeGunSlow : PowerSpeed
{
	Speed 0.6
}

// =============================================================================
//
Actor TimeGunSlow : PowerupGiver
{
	+AUTOACTIVATE
	Inventory.MaxAmount 0
	Powerup.Type "TimeGunSlow"
	Inventory.PickupSound ""
	Powerup.Duration -20
	Powerup.Color "AA 00 00", 0.25

	States
	{
		Spawn:
			TNT1 A 0
			stop
	}
}

// =============================================================================
// Weak debuff
//
Actor PowerTimeGunWeak : PowerDamage
{
	DamageFactor "Fist", 0.7
	DamageFactor "Bullet", 0.7
	DamageFactor "Rifle", 0.7
	DamageFactor "Explosion", 0.7
	DamageFactor "Fire", 0.7
	DamageFactor "Chemical", 0.7
	DamageFactor "Energy", 0.7
	DamageFactor "AntiArmor", 0.7
	DamageFactor "Time", 0.7
	DamageFactor "Disarm", 0.7
	DamageFactor "MassDriver", 0.7
	DamageFactor "Mechstomp", 0.7
	DamageFactor "Monster", 0.7
	Powerup.Duration -15
}

// =============================================================================
//
Actor TimeGunWeak : PowerupGiver
{
	+AUTOACTIVATE
	Inventory.MaxAmount 0
	Powerup.Type "TimeGunWeak"
	Inventory.PickupSound ""
	Powerup.Duration -20
	Powerup.Color "AA 00 00", 0.25
	States
	{
		Spawn:
			TNT1 A 0
			stop
	}
}

// =============================================================================
// HYPAHSPEED
//
Actor PowerTimeGunSpeed : PowerSpeed
{
	Speed 7
}

// =============================================================================
//
Actor TimeGunSpeed : TimeGunBoost
{
	Powerup.Type "TimeGunSpeed"
	Powerup.Duration 0x7FFFFFFF // removed manually
}

// ===================================================
// Base class for effect puffs
//
Actor GoldenPuff
{
	+PUFFONACTORS
	+NOBLOCKMAP
	+NOGRAVITY
	+BLOODLESSIMPACT
	+PUFFGETSOWNER
	RenderStyle Add
	Alpha 0.75
	Translation "192:207=160:167"

	States
	{
		Spawn:
		Melee:
		Crash:
			VOLX ABCDEFEDCBA 3 BRIGHT
			stop
	}
}

// =============================================================================
//
Actor BoostPuffRed : GoldenPuff
{
	DamageType "Time"
	States
	{
		Spawn:
		Melee:
		Crash:
			VOLX AA 0 A_SpawnItem ("TimeGunBoostRed", -1)
			VOLX A 0 A_Explode (5, 16)
			VOLX ABCDEFEDCBA 3 BRIGHT
			stop
	}
}

// =============================================================================
//
Actor BoostPuffBlue : GoldenPuff
{
	DamageType "Time"
	States
	{
		Spawn:
		Melee:
		Crash:
			VOLX AA 0 A_SpawnItem ("TimeGunBoostBlue", -1)
			VOLX A 0 A_Explode (5, 16)
			VOLX ABCDEFEDCBA 3 BRIGHT
			stop
	}
}

// =============================================================================
//
Actor FloatPuffRed : GoldenPuff
{
	DamageType "Time"

	States
	{
		Spawn:
		Melee:
		Crash:
			VOLX AA 0 A_SpawnItem ("TimeGunFloatRed", -1)
			VOLX A 0 A_Explode (5, 16)
			VOLX ABCDEFEDCBA 3 BRIGHT
			stop
	}
}

// =============================================================================
//
Actor FloatPuffBlue : GoldenPuff
{
	DamageType "Time"

	States
	{
		Spawn:
		Melee:
		Crash:
			VOLX AA 0 A_SpawnItem ("TimeGunFloatBlue", -1)
			VOLX A 0 A_Explode (5, 16)
			VOLX ABCDEFEDCBA 3 BRIGHT
			stop
	}
}

// ===================================================
// Flare effects
//
Actor TimeGunFlareExplosion
{
	+CLIENTSIDEONLY +NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("TimeGunFlareGravity", 0, 0, 0, random (-12, 12), random (-12, 12), random (4, 9))
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("TimeGunFlareGravity", 0, 0, 0, random (-12, 12), random (-12, 12), random (4, 9))
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("TimeGunFlareGravity", 0, 0, 0, random (-12, 12), random (-12, 12), random (4, 9))
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("TimeGunFlareGravity", 0, 0, 0, random (-12, 12), random (-12, 12), random (4, 9))
		TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("TimeGunFlareGravity", 0, 0, 0, random (-12, 12), random (-12, 12), random (4, 9))
		TNT1 A 2
		stop
	}
}

// =============================================================================
//
Actor TimeGunFlare : IonRainFlare
{
	+FORCEXYBILLBOARD
	Scale 0.04
	Translation "168:173=147:151", "174:191=148:159", "116:127=148:159"
}

// =============================================================================
//
Actor TimeGunFlareGravity : TimeGunFlare
{
	-NOGRAVITY
	-NOINTERACTION
}

// =============================================================================
// Screen colors
Actor PowerGoldHue : PowerNull {}

Actor GoldHue : PowerupGiver
{
	+INVENTORY.AUTOACTIVATE
	Inventory.MaxAmount 0
	Powerup.Color GoldMap
	Powerup.Duration -1
	Powerup.Type "GoldHue"

	States
	{
		Spawn:
			TNT1 A 0
			stop
	}
}

// =============================================================================
//
Actor TimeGunAmmo : Ammo
{
	+IGNORESKILL
	Inventory.Amount 175
	Inventory.MaxAmount 175
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 175
}
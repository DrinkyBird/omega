// Player Marine
actor Player : DoomPlayer {
	+NOSKIN
	-TELESTOMP
	Player.DisplayName "Commando"
	Player.CrouchSprite "PLYC"
	Player.StartItem "Unarmed", 1
	Player.ForwardMove 0.875
	Player.SideMove 0.875
	DamageFactor "Explosion",        0.3
	DamageFactor "Chemical",         2.0
	DamageFactor "AntiArmor",        0.1
	DamageFactor "Disarm",           0.0 
	DamageFactor "Deconstruction",   0.0
	DamageFactor "MassDriver",       0.3
	DeathSound "generic/death"
	
	States {
	Spawn:
		PLAY A -1
		Loop
	
	See:
		PLAY A 5 
		TNT1 A 0 A_JumpIf(waterlevel > 1, 2)
		TNT1 A 0 A_CheckFloor(1)
		goto Float
		PLAY B 5 
		TNT1 A 0 A_JumpIf(waterlevel > 1, 2)
		TNT1 A 0 A_CheckFloor(1)
		goto Float
		PLAY C 0 A_SpawnItemEx ("MarineStomp", 0, 0, 0, 0, 0, -20) 
		PLAY C 5 
		TNT1 A 0 A_JumpIf(waterlevel > 1, 2)
		TNT1 A 0 A_CheckFloor(1)
		goto Float
		PLAY D 5 
		TNT1 A 0 A_JumpIf(waterlevel > 1, 2)
		TNT1 A 0 A_CheckFloor(1)
		goto Float
		PLAY A 0 A_SpawnItemEx ("MarineStomp", 0, 0, 0, 0, 0, -20) 
		goto Spawn
	
	Float:
		PLAY A 3 A_JumpIf(momz == 0, "See")
		goto See
	
	Missile:
		PLAY E 14
		Goto Spawn
	
	Melee:
		PLAY F 6 BRIGHT
		Goto Missile
	
	MeleeSniper:
		PLAY F 6 BRIGHT
		Goto Missile
	
	Pain:
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	
	Pain.Monster:
		PLAY G 0 A_GiveInventory ("NoHealXP", 1)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	
	Pain.Explosion:
		PLAY G 0 ACS_ExecuteAlways(320, 0)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	
	Pain.Laser:
		PLAY G 0 ACS_ExecuteAlways(320, 0, 4)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	
	Pain.RazorWire:
		PLAY G 0 A_GiveInventory("RazorWireSpeed",1)
		PLAY G 0 ACS_ExecuteAlways(320, 0, 4)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	
	Pain.FireSpecial:
		PLAY G 0 A_GiveInventory ("NoHealXP", 1)
	Pain.Fire:
		PLAY G 0 ACS_ExecuteAlways(320, 0, 1)
		PLAY G 0 ACS_ExecuteAlways(360, 0)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	
	Pain.Chemical:
		PLAY G 0 ACS_ExecuteAlways(320, 0, 2)
		PLAY G 0 ACS_ExecuteAlways(361, 0)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	
	Pain.ChemicalSpecial:
		PLAY G 0 A_GiveInventory ("NoHealXP", 1)
		PLAY G 0 ACS_ExecuteAlways(320, 0, 2)
		PLAY G 4 ACS_ExecuteAlways(310, 0, 2)
		PLAY G 4 A_Pain
		goto Spawn
	
	Death:
		PLAY H 0 
		PLAY H 0 A_JumpIfInventory ("IsKamikaze",1,"Death.IonExplosion")
		PLAY H 0 A_JumpIfInventory ("IsTerrorist",1,"Death.KamikazeExplosion")
		PLAY H 0 A_JumpIfInventory ("IsBombCrate",1,"Death.BombExplosion")
		PLAY H 0 A_JumpIfInventory ("TimeGun", 1, "Death.Time")
		PLAY H 0 A_JumpIfInTargetInventory ("PowerBerserk", 1, "XDeath")
		PLAY H 0 A_JumpIfInTargetInventory ("IsSawing", 1, "XDeath")
	
	DeathGo:
		PLAY H 0 ACS_ExecuteAlways(310, 0, 3)
		PLAY H 0 A_StopSound (CHAN_ITEM)
		PLAY H 0 A_StopSound (CHAN_WEAPON)
		PLAY H 0 A_StopSound (5)
		PLAY H 1 A_Scream
		PLAY H 6 A_TakeInventory("Ingame",1)
		PLAY I 6 Thing_ChangeTID(0,9999)
		PLAY JJJ 0 A_SpawnItemEx("Credits1",random(-4,4),random(-4,4),random(32,48),random(2,4),random(-2,2),random(2,4),0,0,128)
		PLAY J 0 A_SpawnItemEx("PowerupCrate",random(-4,4),random(-4,4),random(32,48),random(2,4),random(-2,2),random(2,4),0,0,230)
		PLAY J 6 A_NoBlocking
		PLAY K 6
		PLAY L 6 A_PlaySound("misc/thud")
		PLAY M 6
		PLAY N -1
		stop
	
	Death.MechStomp:
		PLAY O 0 ACS_ExecuteAlways (923, 0)
		goto XDeath
	
	Death.Falling:
	Death.Suicide:
	XDeath:
		PLAY O 0 
		PLAY O 0 A_JumpIfInventory ("IsKamikaze", 1, "Death.IonExplosion")
		PLAY O 0 A_JumpIfInventory ("IsTerrorist", 1, "Death.KamikazeExplosion")
		PLAY H 0 A_JumpIfInventory ("IsBombCrate",1,"Death.BombExplosion")
		PLAY O 0 A_JumpIfInventory ("TimeGun", 1, "Death.Time")
		goto XDeathGo
	
	XDeathGo:
		PLAY O 0 ACS_ExecuteAlways(310, 0, 3)
		PLAY O 0 A_StopSound (CHAN_ITEM)
		PLAY O 0 A_StopSound (CHAN_WEAPON)
		PLAY O 0 A_StopSound (5)
		PLAY O 4
		PLAY P 0 A_NoBlocking
		PLAY PPP 0 A_SpawnItemEx ("Credits1",random(-4,4),random(-4,4),random(32,48),random(-4,4),random(-4,4),random(4,8),0,0,128)
		PLAY P 0 A_SpawnItemEx ("PowerupCrate",random(-4,4),random(-4,4),random(32,48),random(2,4),random(-2,2),random(2,4),0,0,230)
		PLAY P 0 A_TakeInventory ("Ingame",1)
		PLAY P 0 A_Scream
		PLAY P 0 A_PlaySound ("misc/gib", 7)
		PLAY P 0 A_PlaySound ("misc/gib", 6)
		PLAY P 4 A_SpawnItemEx ("MarineGoreExplosion")
		PLAY Q 4 A_SpawnItemEx ("MarineGoreExplosion2")
		PLAY R 4 A_SpawnItemEx ("MarineGoreExplosion3")
		PLAY STUV 4
		PLAY W -1
		stop
	
	Death.Explosion:
		PLAY H 1
		PLAY H 0 A_Jump(128,"Death")
		goto XDeath
	
	Death.FireSpecial:
	Death.Fire:
		TNT1 A 0 A_JumpIfInventory("IsInSpawnRoom",1,"FireAlt")
		PLAY H 0 A_JumpIfInventory ("IsKamikaze",1,"Death.IonExplosion")
		PLAY H 0 A_JumpIfInventory ("TimeGun", 1, "Death.Time")
		PLAY H 0 A_JumpIfInventory ("IsBombCrate",1,"Death.BombExplosion")
		PLAY H 0 A_JumpIfInventory ("IsFemale",1,"DeathF.Fire")
		PLAY H 0 A_StopSound (CHAN_ITEM)
		PLAY H 0 A_StopSound (CHAN_WEAPON)
		PLAY H 0 A_StopSound (5)
		PLAY H 0 ACS_ExecuteAlways(310,0,3,0,0)
		PLAY H 1 Thing_ChangeTID(0,9999)
		PLAY H 0 A_TakeInventory("Ingame",1)
		DISR K 0 A_SpawnItemEx ("BurningCorpse", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		DISR K 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0,0)
		stop
	
	FireAlt:
		PLAY H 1 Thing_ChangeTID(0,9999)
		TNT1 A 0 A_TakeInventory("Ingame",1)
		TNT1 A 0 A_SpawnItemEx("BurningCorpseReduced", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		TNT1 A 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0,0)
		stop
	
	Death.ChemicalSpecial:
	Death.Chemical:
		TNT1 AA 0 A_JumpIfInventory ("IsInSpawnRoom", 1, "ChemFireAlt")
		PLAY H 0 A_JumpIfInventory ("IsKamikaze", 1, "Death.IonExplosion")
		PLAY H 0 A_JumpIfInventory ("TimeGun", 1, "Death.Time")
		PLAY H 0 A_JumpIfInventory ("IsBombCrate", 1, "Death.BombExplosion")
		PLAY H 0 A_JumpIfInventory ("IsFemale", 1, "DeathF.Chemical")
		PLAY H 0 A_StopSound (CHAN_ITEM)
		PLAY H 0 A_StopSound (CHAN_WEAPON)
		PLAY H 0 A_StopSound (CHAN_VOICE)
		PLAY H 0 A_StopSound (5)
		PLAY H 0 ACS_ExecuteAlways(310, 0, 3)
		PLAY H 1 Thing_ChangeTID(0,9999)
		BURN A 0 A_StopSoundEx("Item")
		PLAY H 0 A_TakeInventory("Ingame",1)
		DISR K 0 A_SpawnItemEx ("ChemicalCorpse", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		DISR K 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0,0)
		stop
	
	ChemFireAlt:
		PLAY H 0 A_StopSound (CHAN_ITEM)
		PLAY H 0 A_StopSound (CHAN_WEAPON)
		PLAY H 0 A_StopSound (CHAN_VOICE)
		PLAY H 0 A_StopSound (5)
		PLAY H 0 ACS_ExecuteAlways(310,0,3,0,0)
		PLAY H 1 Thing_ChangeTID(0,9999)
		TNT1 A 0 A_TakeInventory("Ingame",1)
		TNT1 A 0 A_SpawnItemEx("ChemicalCorpseReduced", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		TNT1 A 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0,0)
		Stop
	
	DeathF.Fire:
		TNT1 AA 0 A_JumpIfInventory("IsInSpawnRoom",1,"FireAlt")
		PLAY H 1 Thing_ChangeTID(0,9999)
		PLAY H 0 A_TakeInventory("Ingame",1)
		DISR K 0 A_SpawnItemEx ("BurningCorpseFemale", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		DISR K 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0,0)
		stop
	
	DeathF.Chemical:
		TNT1 AA 0 A_JumpIfInventory("IsInSpawnRoom",1,"ChemFireAlt")
		PLAY H 1 Thing_ChangeTID(0,9999)
		PLAY H 0 A_TakeInventory("Ingame",1)
		DISR K 0 A_SpawnItemEx ("ChemicalCorpseFemale", 0,0,0,0,0,0,0, SXF_NOCHECKPOSITION)
		DISR K 0 A_NoBlocking
		PLAY N -1 A_SetTranslucent(0.0,0)
		stop
	
	Death.IonExplosion:
		PLAY H 0
		PLAY H 0 A_JumpIfInventory ("IsRed", 1, "Death.NukeExplosion")
		PLAY N 0 A_SpawnItemEx("BlueStrikeKamikaze", 0, 0, 32)
		goto XDeathGo
	
	Death.NukeExplosion:
		PLAY H 0
		PLAY N 0 A_SpawnItemEx("NukeKamikaze", 0, 0, 32)
		goto XDeathGo
	
	Death.KamikazeExplosion:
		PLAY H 0
		PLAY N 0 A_SpawnItemEx("KamikazeExplosion")
		goto XDeathGo
	
	Death.BombExplosion:
		PLAY H 0
		PLAY N 0 A_SpawnItemEx("BombExplosion")
		goto XDeathGo
	
	Death.Time:
		PLAY G 0 A_JumpIfInventory("IsKamikaze",1,"Death.IonExplosion")
		PLAY H 0 A_JumpIfInventory ("IsBombCrate",1,"Death.BombExplosion")
		PLAY G 0
		PLAY G 0 A_NoBlocking
		PLAY G 0 A_StopSound (CHAN_ITEM)
		PLAY G 0 A_StopSound (CHAN_WEAPON)
		PLAY G 0 A_StopSound (5)
		PLAY G 0 A_TakeInventory("Ingame",1)
		PLAY G 0 Thing_ChangeTID(0,9999)
		PLAY G 0 A_Stop
		PLAY G 0 A_NoGravity
		PLAY GGG 0 A_SpawnItemEx("Credits1",random(-4,4),random(-4,4),random(32,48),random(2,4),random(-2,2),random(2,4),0,0,128)
		PLAY G 0 A_SpawnItemEx("PowerupCrate",random(-4,4),random(-4,4),random(32,48),random(2,4),random(-2,2),random(2,4),0,0,230)
		PLAY G 2 A_PlaySound("weapons/timedeath")
		PLAY G 2 Bright A_SetTranslucent(1,1)
		PLAY G 2 Bright A_SetTranslucent(0.95,1)
		PLAY G 2 Bright A_SetTranslucent(0.90,1)
		PLAY G 2 Bright A_SetTranslucent(0.85,1)
		PLAY G 2 Bright A_SetTranslucent(0.80,1)
		PLAY G 2 Bright A_SetTranslucent(0.75,1)
		PLAY G 2 Bright A_SetTranslucent(0.70,1)
		PLAY G 2 Bright A_SetTranslucent(0.65,1)
		PLAY G 2 Bright A_SetTranslucent(0.60,1)
		PLAY G 2 Bright A_SetTranslucent(0.45,1)
		PLAY G 2 Bright A_SetTranslucent(0.40,1)
		PLAY G 2 Bright A_SetTranslucent(0.25,1)
		PLAY G 2 Bright A_SetTranslucent(0.20,1)
		PLAY G 2 Bright A_SetTranslucent(0.15,1)
		PLAY G 2 Bright A_SetTranslucent(0.10,1)
		PLAY G 2 Bright A_SetTranslucent(0.05,1)
		PLAY G 2 Bright A_SetTranslucent(0.00,1)
		PLAY N -1 A_SetTranslucent(0.0,0)
		Stop
	}
}

actor MarineGoreExplosion {
	+CLIENTSIDEONLY
	+NOINTERACTION
	states {
	Spawn:
		TNT1 AAAA 0 A_SpawnItemEx("GibExplosion", 0, 0, 32)
		TNT1 A 0 A_SpawnItemEx("Giblet1",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		TNT1 A 0 A_SpawnItemEx("Giblet5",0,0,random(4,40),random(-2,4),random(-6,6),random(-1,10),random(-32,32))
		TNT1 A 0 A_SpawnItemEx("Giblet2",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		TNT1 A 0 A_SpawnItemEx("Giblet3",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		TNT1 A 3
		stop
	}
}

actor MarineGoreExplosion2 {
	+CLIENTSIDEONLY
	+NOINTERACTION
	states {
	Spawn:
		TNT1 AAAA 0 A_SpawnItemEx ("GibExplosion", 0, 0, 32)
		TNT1 A 0 A_SpawnItemEx ("Giblet4",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		TNT1 A 0 A_SpawnItemEx ("Giblet5",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		TNT1 A 0 A_SpawnItemEx ("Giblet6",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		TNT1 A 0 A_SpawnItemEx ("AOWBrainStem", 0, 0, 50, random(-4, 4), random(-4, 4), random(7, 9))
		TNT1 A 3
		stop
	}
}


actor MarineGoreExplosion3 {
	+CLIENTSIDEONLY
	+NOINTERACTION
	states {
	Spawn:
		TNT1 AAAA 0 A_SpawnItemEx("GibExplosion", 0, 0, 32)
		TNT1 A 0 A_SpawnItemEx("Giblet7",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		TNT1 A 0 A_SpawnItemEx("Giblet8",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		TNT1 A 0 A_SpawnItemEx("Giblet7",0,0,random(4,40),random(-2,4),random(-6,6),random(-2,10),random(-32,32))
		TNT1 A 3
		stop
	}
}


actor MarineStomp  {
	PROJECTILE
	+NOBLOOD
	+CLIENTSIDEONLY
	Damage 0
	Radius 10
	Speed 20
	DeathSound "misc/footstep"
	
	States {
	Spawn:
		TNT1 A 1
		stop
	Death:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInTargetInventory ("StealthMode", 1, 2)
		TNT1 A 0 A_SpawnItem("PlayerStepSmokePuff")
		TNT1 A 1
		stop
	}
}
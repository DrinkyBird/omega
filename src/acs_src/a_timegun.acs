// ===================================================
// ------------------------------------------ TIME GUN
// ===================================================

#define NUM_TIME_GUN_MODES 3

// [Dusk] Time gun effect messages and altfire rotation (in one script o.O)
// action == 0 => intel message
// action == 1 => altfire rotation
// action == 2 => reward check
script SC_TIMEGUN( int action, int type )
{	if( action == 1 )
	{	// Rotation
		SetInventory ("TimeGunRotation", (CheckInventory ("TimeGunRotation") + 1) % (NUM_TIME_GUN_MODES));
		ActivatorSound( "switch/pulse", 127 );
		terminate;
	}

	if( action == 2 )
	{	// [Dusk] Check for a possible reward.
		// Boosting teammates gives you 1 bonus,
		// weakening enemies gets you 2 bonus.
		int ttid = GetTargetInfo( TARGETINFO_TID );

		if( ttid == ActivatorTID() || ttid < TID_Player || ttid >= TID_Player + MAXPLAYERS )
		{	// No valid target
			terminate;
		}

		int bonus = 1;

		if( GetTargetInfo( TARGETINFO_Team ) != PlayerTeam() )
			bonus = 2;

		int pn = PlayerNumber();

		if( MedicBonus[pn] < GetCVAR( "aow_medicbonusneed" ))
		{	MedicBonus[pn] += 40 * bonus;
		}
		else
		{	MedicBonus[pn] = 0;
			AddExperience( 50 );
			LocalAmbientSound( "c4/use", 127 );
			GivePoints( 1 );
		}

		terminate;
	}

	SetFont( "SMALLFONT" );

	switch( type )
	{	case 0: // boost
			IntelMessage( "\cIYou are turbo!" );
			break;

		case 1: // float
			IntelMessage( "\cIYou feel lighter!" );
			break;

		case 2: // slow
			IntelMessage( "\cGYou have been slowed down!" );
			break;

		case 3: // weaken
			IntelMessage( "\cGYou have been weakened!" );
			break;
	}
}

// ------------
// Time gun HUD
script SC_CL_TimeGunHUD( void ) NET CLIENTSIDE
{	while( CheckWeapon( "TimeGun" ) && GetHealth() >= 0 )
	{	SetFont( "SMALLFONT" );
		HudMessage(
			s: TimeHUDColor( 0, 0 ), s: "BOOST ", s: TimeHUDColor( 0, 1 ), s: " / ", s: TimeHUDColor( 0, 2 ), s: "SLOW\n",
			s: TimeHUDColor( 1, 0 ), s: "FLOAT ", s: TimeHUDColor( 1, 1 ), s: " / ", s: TimeHUDColor( 1, 2 ), s: "WEAKEN\n",
			s: TimeHUDColor( 2, 0 ), s: "HYPERSPEED\n";
			HUDMSG_PLAIN, 0, CR_WHITE, -0.8, -0.6, 0.2 );

		delay( 5 );
	}
}

function str TimeHUDColor( int type, int side )
{	if( CheckInventory( "TimeGunRotation" ) == type )
	{	// selected
		switch( type )
		{	case 0:
			case 1:
				switch( side )
				{	case 0: return "\cD";
					case 1: return "\cC";
					case 2: return "\cG";
				}

			case 2:
				return "\cF";
		}
	}
	else
	{	// not selected
		switch( type )
		{	case 0:
			case 1:
				switch( side )
				{	case 0: return "\cQ";
					case 1: return "\cU";
					case 2: return "\cR";
				}

			case 2:
				return "\cK";
		}
	}

	return "";
}
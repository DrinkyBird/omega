// MONEY RELATED SCRIPTS
function int IncrCredits (int p, int amount) {
	if (IsAdmin[PlayerNumber()])
		return false;
	
	Credits[p] += amount;
	
	if (Credits[p] > GetCVAR ("aow_creditlimit") && GetCVAR ("aow_creditlimit") > 0)
	{
		Credits[p] = GetCVAR ("aow_creditlimit");
		if(!CheckInventory("IsInSpawnRoom") && GetTeamProperty (PlayerTeam (), TPROP_NumPlayers) > 4 && GetCVAR ("aow_greedkill") >= 1)
		{
			SetFont("BigFont");
			Hudmessage(s:"DONATE YOU GREEDY BASTARD!"; HUDMSG_FADEOUT, 480+PlayerNumber(), CR_Green, 0.5, 0.55, 2.0, 1.0);
			DamageThing(10000);
		}
	}
	
	return true;
}

function int DecrCredits (int p, int amount) {
	if (IsAdmin[PlayerNumber()])
		return false;
	
	Credits[p] -= amount;
	if (Credits[p] < 0)
		Credits[p] = 0;
	
	return true;
}

Function int SpendCredits (int amount) {
	if (IsAdmin[PlayerNumber()])
		return -1;
	
	int delta = Discount(amount);
	amount -= delta;
	
	DecrCredits (PlayerNumber (), amount);
	
	SETFONT("BigFont");
	if (delta == 0)
		HudMessage(s:"\cJYou have spent \cQ$\cD",d:amount; HUDMSG_FADEOUT, 480+PlayerNumber(), CR_Green, 0.5, 0.55, 2.0, 1.0);
	else
		HudMessage(s:"\cJYou have spent \cQ$\cD",d:amount, s:" \cF[\cQ-$\cD", d:delta, s:"\cF]";
			HUDMSG_FADEOUT, 480+PlayerNumber(), CR_Green, 0.5, 0.55, 2.0, 1.0);
		
	return amount;
}

function void AwardCredits (int amount) {
	int p = PlayerNumber ();
	if (IsAdmin[p])
		return;
	
	IncrCredits (p, amount);
	SetFont ("SMALLFONT");
	HudMessage (s:"You have been awarded with \cQ$\cD", d:amount;
		HUDMSG_FADEOUT, 15000, CR_WHITE,
		INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
}

// [Dusk] cleaner version :|
function void GiveCredits (int p, int amount) {
	if (p < 0)
		p = PlayerNumber();
	
	if (IsAdmin[p])
		return;
	
	IncrCredits (p, amount);
}
 
function int GetCredits (int p) {
	if (!PlayerInGame (p))
		return 0;
	
	return Credits[p];
}

// testing script
Script 32 (int amount, int i) {
	if (IsAdmin [i])
		terminate;
	
	GiveCredits (i, amount);
	HudMessageBold (s:"\cRServer \cJhas given \cQ$\cD", d:amount, s:" \cJto ", n:i+1;
		HUDMSG_FADEOUT, 15000, CR_WHITE,
		INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
}

// Backpacks and credit items use this to give the player credits
script 321 (int amount) {
	if (IsAdmin[PlayerNumber ()])
		terminate;
	
	GiveCredits (PlayerNumber (), amount);
}

//Price check
script 330 (int Price) {
	int delta = Discount(Price);
	Price -= delta;
	
	SetFont("BIGFONT");
	if (delta != 0)
		HudMessage(s:"\cJDiscount \cQ-$\cD", d:delta, s:"\cF = \cQ$\cD", d:Price;
			HUDMSG_FADEOUT, 480+PlayerNumber(), CR_Green, 0.25, 0.1, 2.0, 1.0);
	
	SetResultValue (Price);
}

// CREDIT BONUS
Script 326 OPEN {
	while (CreditAwardTime <= 0)
		delay (5*35);
	
	delay (35 * 60 * CreditAwardTime);
	
	for (int i = 0; i < MAXPLAYERS; i++)
		if (PlayerInGame (i) && !IsAdmin[i])
			IncrCredits (i, CreditAwardAmount);
	
	AmbientSound ("powerup/credsound", 127);
	SetFont ("SMALLFONT");
	HudMessageBold (s:"Credit bonus! You all get \cQ$\cD", d:CreditAwardAmount, s:"\cJ!";
		HUDMSG_FADEOUT, 15000, CR_WHITE,
		INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
	
	restart;
}

function int Discount (int amount) {
	if (Rank[PlayerNumber()] == RANK_PRIVATE)
		return 0;
	
	int delta, factor;
	
	if (amount < 200 && amount > 0)
		factor = 10;
	else if (amount >= 200 && amount < 400)
		factor = 15;
	else if (amount >= 400 && amount < 800)
		factor = 30;
	else if (amount >= 800 && amount < 1600)
		factor = 50;
	else if (amount > 1600)
		factor = 75;
	
	switch (Rank[PlayerNumber()]) {
	case RANK_CORPORAL:
	case RANK_SERGEANT:
		delta = factor; 
		break;
	
	case RANK_MASTERSGT:
	case RANK_OFFICER:
		delta = factor * 2;
		break;
	
	case RANK_LIEUTENANT:
	case RANK_MAJOR:
	case RANK_COMMANDO:
		delta = factor * 4;
		break;
	}
	
	return delta;
}

// ------------------------------------------------------------------------------------
function void SetCredits (int p, int amount) {
	if (IsAdmin[PlayerNumber()])
		return;
	
	if (amount < 0)
		amount = 0;
	
	if (amount > 10000)
		amount = 10000;
	
	Credits[p] = amount;
}

Function int AddCredits (int Player, int NewCredits)
{
	if (IsAdmin[PlayerNumber()])
		return -1;
	
	if (IncrCredits (PlayerNumber (), NewCredits)) {
		SetFont ("SMALLFONT");
		HudMessage (s:"You have been awarded with \cQ$\cD", d:NewCredits;
		HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
	}
	
	return NewCredits;
}

Function int RefundCredits (int NewCredits) {
	if (IsAdmin[PlayerNumber()])
		return -1;
	
	if (IncrCredits (PlayerNumber (), NewCredits)) {
		SetFont("SMALLFONT");
		HudMessage (s:"You have been refunded with \cQ$\cD", d:NewCredits;
		HUDMSG_FADEOUT, 15000, CR_WHITE,
				INTEL_XPOS, INTEL_YPOS, 2.0, 1.0);
	}
	
	return NewCredits;
}
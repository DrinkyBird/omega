/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * All Out War 2 team balancer
 * Credit to VoltlocK for originally creating the engine
 * With modifications by Eruanna, Dusk and the Omega Team
 *
 * You may use portions of this script in your project as long as you give
 * credit where credit is due. Please don't be lame and just copy-paste any
 * of this and call it your own. Thanks!
 */

// [Dusk] Rewritten this for the most part..
function void CheckTeamBalance( void )
{	int i, delta, lesserteam, switchee, switcheetime;
	int PlayerStayTics = GetCVAR( "aow_teamstaytime" ) * MINUTETICS;

	// [Dusk] Allow the team switcher be disabled
	if( GetCVar( "aow_noteamswitcher" ))
		return;

	while( true )
	{	delta = RedCount() - BlueCount();

		if( abs( delta ) <= 1 )
			break; // teams are even enough

		lesserteam = cond( delta > 0, TEAM_BLUE, TEAM_RED );

		// Find someone to switch
		switchee = MAXPLAYERS;
		switcheetime = 0;

		for( i = 0; i < MAXPLAYERS; i++ )
		{	if( !PlayerInGame( i ) || IsAdmin[i] || GetPlayerTeam( i ) == lesserteam )
				continue; // not a player on the greater team

			// Find the player with least start time. The player also must have been in
			// the game for a certain amount of time, after that, he's locked into his team
			// and cannot be switched.
			if( StartTime[i] > switcheetime && Timer() - StartTime[i] < PlayerStayTics )
			{	switchee = i;
				switcheetime = StartTime[i];
			}
		}

		// If there's nobody to switch, we're done here. Though this shouldn't happen...
		if( switchee == MAXPLAYERS )
			return;

		Log( s:"Team auto-switcher: switching out ", n:switchee + 1,
			s:"\c- to ", s:TeamNames[lesserteam] );

		ChangePlayerTeam( switchee, lesserteam );

		if( ( Timer() - StartTime[i] ) > TICRATE )
		{	// If the player was already in-game for a bit, tell him why
			// he's suddenly being switched out.
			SetActivator( TID_Player + switchee );
			BigMessage( "You have been switched to even out teams." );
		}

		SetActivator( -1 );
	};
}
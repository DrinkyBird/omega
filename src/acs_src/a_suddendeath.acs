/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
 * All Out War 2 sudden death script
 * Credit to VoltlocK for originally creating the engine
 * With modifications by Eruanna, Dusk and the Omega Team
 *
 * You may use portions of this script in your project as long as you give
 * credit where credit is due. Please don't be lame and just copy-paste any
 * of this and call it your own. Thanks!
 */

script 995 OPEN
{	Delay( 60 * ( 35 * 5 ));
	CrateTime = False;
}

script 998 OPEN
{	delay( 5 );

	while( Timer() < GetCVAR( "aow_suddendeathtime" ) * 35 * 60 ) delay( 35 * 15 );

	ACS_Execute( 997, 0 );
}

script 997( void )
{	if( EventScripts ) ACS_ExecuteAlways( 960, 0, ACTION_SUDDENDEATH, TEAM_NONE );

	GiveActorInventory( 100, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 101, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 102, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 103, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 104, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 105, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 106, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 107, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 108, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 114, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 115, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 200, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 201, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 202, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 203, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 204, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 205, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 206, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 207, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 208, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 214, "SuddenDeathVulnerability", 1 );
	GiveActorInventory( 215, "SuddenDeathVulnerability", 1 );

	if( Timer() > 5 * 35 + 10 )
	{	if( random( 0, 1 ))
			SetMusic( "razor-ub" );
		else
			SetMusic( "razorub2" );
	}

Log( s: "=== Sudden Death begins ===" );

	// Nukes for everybody!
	GiveToAll( "BeaconItem", 1 );

	BigMessageBold( "\cgSUDDEN DEATH!\n\nNo more repairs can be done\nEvery death counts!" );

	if( !TicketPool )
	{	// init tickets
		Tickets[TEAM_BLUE] = StartingTickets;
		Tickets[TEAM_RED] = StartingTickets;
		SetActorSpeed( TID_TICKETCOUNTER_BLUE, StartingTickets );
		SetActorSpeed( TID_TICKETCOUNTER_RED, StartingTickets );
	}

	SuddenDeath = true;

	// Update the building HUD now.
	DrawBuildingHUD();
	SetActivator( -1 );

	// Every minute flash some intel message
	while( 1 )
	{	IntelMessageBold( "Sudden death!" );
		delay( 35 * 60 );
	}
}

// [Dusk] Checks tickets and ends the game if tickets are out
// The activator is to be the killer!
function void TakeTickets( int team, int count )
{	if( !SuddenDeath && !TicketPool )
		return;

	Tickets[team] -= count;

	// inform clients of the new ticket values by changing the counters'
	// speed. clients use these speeds to display ticket values.
	if( GetActorSpeed( TID_TICKETCOUNTER_RED ) != Tickets[TEAM_RED] )
		SetActorSpeed( TID_TICKETCOUNTER_RED, Tickets[TEAM_RED] );

	if( GetActorSpeed( TID_TICKETCOUNTER_BLUE ) != Tickets[TEAM_BLUE] )
		SetActorSpeed( TID_TICKETCOUNTER_BLUE, Tickets[TEAM_BLUE] );

	// Update the HUD now.
	DrawBuildingHud();

	// handle ticket loss
	int d;

	if( Tickets[TEAM_BLUE] <= 0 )
	{	d = BlueScore() - RedScore();

		if( d < 0 )
			d = 0;

		SetFont( "BIGFONT" );
		HudMessageBold( s: "\cHBlue Team\cJ lose out of ticket loss";
			HUDMSG_FADEOUT, 0, CR_YELLOW, 0.5, 0.3, 2.0, 1.0 );
		Log( s: "Blue loses out of ticket loss" );

		GameOver = true;
		ConsoleCommand( "pointlimit 1" );
		Team_GivePoints( TEAM_RED, d + 50, 0 );
	}
	else if( Tickets[TEAM_RED] <= 0 )
	{	d = RedScore() - BlueScore();

		if( d < 0 )
			d = 0;

		SetFont( "BIGFONT" );
		HudMessageBold( s: "\cGRed Team\cJ loses out of ticket loss";
			HUDMSG_FADEOUT, 0, CR_YELLOW, 0.5, 0.3, 2.0, 1.0 );
		Log( s: "Red loses out of ticket loss" );

		GameOver = true;
		ConsoleCommand( "pointlimit 1" );
		Team_GivePoints( TEAM_BLUE, d + 50, 0 );
	}
}